module RealS4Derivations where

open import Prelude
open import Category
open import List
open import ListLemmas
open import ListConcatenation
open import AllList
open import S4Propositions
import IPLPropositions as IPL
import IPLDerivations as IPL


--------------------------------------------------------------------------------


record Assert : Set
  where
    constructor âŸªâŠ«_âŸ«
    field
      A : Prop


--------------------------------------------------------------------------------


infix 3 _âŠ¢_valid[_]
data _âŠ¢_valid[_] : List Assert â†’ Prop â†’ List Prop â†’ Set
  where
    var : âˆ€ {A Î” Î“} â†’ Î“ âˆ‹ A
                    â†’ Î” âŠ¢ A valid[ Î“ ]

    lam : âˆ€ {A B Î” Î“} â†’ Î” âŠ¢ B valid[ Î“ , A ]
                      â†’ Î” âŠ¢ A âŠƒ B valid[ Î“ ]

    app : âˆ€ {A B Î” Î“} â†’ Î” âŠ¢ A âŠƒ B valid[ Î“ ] â†’ Î” âŠ¢ A valid[ Î“ ]
                      â†’ Î” âŠ¢ B valid[ Î“ ]

    mvar : âˆ€ {A Î” Î“} â†’ Î” âˆ‹ âŸªâŠ« A âŸ«
                     â†’ Î” âŠ¢ A valid[ Î“ ]

    box : âˆ€ {A Î” Î“} â†’ Î” âŠ¢ A valid[ âˆ™ ]
                    â†’ Î” âŠ¢ â–¡ A valid[ Î“ ]

    letbox : âˆ€ {A B Î” Î“} â†’ Î” âŠ¢ â–¡ A valid[ Î“ ] â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ B valid[ Î“ ]
                         â†’ Î” âŠ¢ B valid[ Î“ ]


infix 3 _âŠ¢_allvalid[_]
_âŠ¢_allvalid[_] : List Assert â†’ List Prop â†’ List Prop â†’ Set
Î” âŠ¢ Î allvalid[ Î“ ] = All (\ A â†’ Î” âŠ¢ A valid[ Î“ ]) Î


infix 3 _âŠ¢_allvalid*
_âŠ¢_allvalid* : List Assert â†’ List Assert â†’ Set
Î” âŠ¢ Î allvalid* = All (\ { âŸªâŠ« A âŸ« â†’ Î” âŠ¢ A valid[ âˆ™ ] }) Î


--------------------------------------------------------------------------------


ren : âˆ€ {Î” Î“ Î“â€² A} â†’ Î“â€² âŠ‡ Î“ â†’ Î” âŠ¢ A valid[ Î“ ]
                   â†’ Î” âŠ¢ A valid[ Î“â€² ]
ren Î· (var i)      = var (renâˆ‹ Î· i)
ren Î· (lam ğ’Ÿ)      = lam (ren (keep Î·) ğ’Ÿ)
ren Î· (app ğ’Ÿ â„°)    = app (ren Î· ğ’Ÿ) (ren Î· â„°)
ren Î· (mvar i)     = mvar i
ren Î· (box ğ’Ÿ)      = box ğ’Ÿ
ren Î· (letbox ğ’Ÿ â„°) = letbox (ren Î· ğ’Ÿ) (ren Î· â„°)


rens : âˆ€ {Î” Î“ Î“â€² Î} â†’ Î“â€² âŠ‡ Î“ â†’ Î” âŠ¢ Î allvalid[ Î“ ]
                    â†’ Î” âŠ¢ Î allvalid[ Î“â€² ]
rens Î· Î¾ = maps (ren Î·) Î¾


--------------------------------------------------------------------------------


mren : âˆ€ {Î” Î”â€² Î“ A} â†’ Î”â€² âŠ‡ Î” â†’ Î” âŠ¢ A valid[ Î“ ]
                    â†’ Î”â€² âŠ¢ A valid[ Î“ ]
mren Î· (var i)      = var i
mren Î· (lam ğ’Ÿ)      = lam (mren Î· ğ’Ÿ)
mren Î· (app ğ’Ÿ â„°)    = app (mren Î· ğ’Ÿ) (mren Î· â„°)
mren Î· (mvar i)     = mvar (renâˆ‹ Î· i)
mren Î· (box ğ’Ÿ)      = box (mren Î· ğ’Ÿ)
mren Î· (letbox ğ’Ÿ â„°) = letbox (mren Î· ğ’Ÿ) (mren (keep Î·) â„°)


mrens : âˆ€ {Î” Î”â€² Î“ Î} â†’ Î”â€² âŠ‡ Î” â†’ Î” âŠ¢ Î allvalid[ Î“ ]
                     â†’ Î”â€² âŠ¢ Î allvalid[ Î“ ]
mrens Î· Î¾ = maps (mren Î·) Î¾


mrens* : âˆ€ {Î” Î”â€² Î} â†’ Î”â€² âŠ‡ Î” â†’ Î” âŠ¢ Î allvalid*
                    â†’ Î”â€² âŠ¢ Î allvalid*
mrens* Î· Î¾ = maps (mren Î·) Î¾


--------------------------------------------------------------------------------


wk : âˆ€ {B A Î” Î“} â†’ Î” âŠ¢ A valid[ Î“ ]
                 â†’ Î” âŠ¢ A valid[ Î“ , B ]
wk ğ’Ÿ = ren (drop id) ğ’Ÿ


vz : âˆ€ {A Î” Î“} â†’ Î” âŠ¢ A valid[ Î“ , A ]
vz = var zero


wks : âˆ€ {A Î” Î“ Î} â†’ Î” âŠ¢ Î allvalid[ Î“ ]
                  â†’ Î” âŠ¢ Î allvalid[ Î“ , A ]
wks Î¾ = rens (drop id) Î¾


lifts : âˆ€ {A Î” Î“ Î} â†’ Î” âŠ¢ Î allvalid[ Î“ ]
                    â†’ Î” âŠ¢ Î , A allvalid[ Î“ , A ]
lifts Î¾ = wks Î¾ , vz


vars : âˆ€ {Î” Î“ Î“â€²} â†’ Î“â€² âŠ‡ Î“
                  â†’ Î” âŠ¢ Î“ allvalid[ Î“â€² ]
vars done     = âˆ™
vars (drop Î·) = wks (vars Î·)
vars (keep Î·) = lifts (vars Î·)


ids : âˆ€ {Î” Î“} â†’ Î” âŠ¢ Î“ allvalid[ Î“ ]
ids = vars id


--------------------------------------------------------------------------------


mwk : âˆ€ {B A Î” Î“} â†’ Î” âŠ¢ A valid[ Î“ ]
                  â†’ Î” , âŸªâŠ« B âŸ« âŠ¢ A valid[ Î“ ]
mwk ğ’Ÿ = mren (drop id) ğ’Ÿ


mvz : âˆ€ {A Î” Î“} â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ A valid[ Î“ ]
mvz = mvar zero


mwks : âˆ€ {A Î” Î“ Î} â†’ Î” âŠ¢ Î allvalid[ Î“ ]
                   â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ Î allvalid[ Î“ ]
mwks Î¾ = mrens (drop id) Î¾


mwks* : âˆ€ {A Î” Î} â†’ Î” âŠ¢ Î allvalid*
                  â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ Î allvalid*
mwks* Î¾ = mrens* (drop id) Î¾


mlifts* : âˆ€ {A Î” Î} â†’ Î” âŠ¢ Î allvalid*
                    â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ Î , âŸªâŠ« A âŸ« allvalid*
mlifts* Î¾ = mwks* Î¾ , mvz


mvars* : âˆ€ {Î” Î”â€²} â†’ Î”â€² âŠ‡ Î”
                  â†’ Î”â€² âŠ¢ Î” allvalid*
mvars* done     = âˆ™
mvars* (drop Î·) = mwks* (mvars* Î·)
mvars* (keep Î·) = mlifts* (mvars* Î·)


mids* : âˆ€ {Î”} â†’ Î” âŠ¢ Î” allvalid*
mids* = mvars* id


--------------------------------------------------------------------------------


sub : âˆ€ {Î” Î“ Î A} â†’ Î” âŠ¢ Î allvalid[ Î“ ] â†’ Î” âŠ¢ A valid[ Î ]
                  â†’ Î” âŠ¢ A valid[ Î“ ]
sub Î¾ (var i)      = get Î¾ i
sub Î¾ (lam ğ’Ÿ)      = lam (sub (lifts Î¾) ğ’Ÿ)
sub Î¾ (app ğ’Ÿ â„°)    = app (sub Î¾ ğ’Ÿ) (sub Î¾ â„°)
sub Î¾ (mvar i)     = mvar i
sub Î¾ (box ğ’Ÿ)      = box ğ’Ÿ
sub Î¾ (letbox ğ’Ÿ â„°) = letbox (sub Î¾ ğ’Ÿ) (sub (mwks Î¾) â„°)


subs : âˆ€ {Î” Î“ Î Î¨} â†’ Î” âŠ¢ Î allvalid[ Î“ ] â†’ Î” âŠ¢ Î¨ allvalid[ Î ]
                   â†’ Î” âŠ¢ Î¨ allvalid[ Î“ ]
subs Î¾ Ïˆ = maps (sub Î¾) Ïˆ


--------------------------------------------------------------------------------


msub : âˆ€ {Î” Î“ Î A} â†’ Î” âŠ¢ Î allvalid* â†’ Î âŠ¢ A valid[ Î“ ]
                   â†’ Î” âŠ¢ A valid[ Î“ ]
msub Î¾ (var i)      = var i
msub Î¾ (lam ğ’Ÿ)      = lam (msub Î¾ ğ’Ÿ)
msub Î¾ (app ğ’Ÿ â„°)    = app (msub Î¾ ğ’Ÿ) (msub Î¾ â„°)
msub Î¾ (mvar i)     = sub âˆ™ (get Î¾ i)
msub Î¾ (box ğ’Ÿ)      = box (msub Î¾ ğ’Ÿ)
msub Î¾ (letbox ğ’Ÿ â„°) = letbox (msub Î¾ ğ’Ÿ) (msub (mlifts* Î¾) â„°)


msubs : âˆ€ {Î” Î“ Î Î¨} â†’ Î” âŠ¢ Î allvalid* â†’ Î âŠ¢ Î¨ allvalid[ Î“ ]
                    â†’ Î” âŠ¢ Î¨ allvalid[ Î“ ]
msubs Î¾ Ïˆ = maps (msub Î¾) Ïˆ


msubs* : âˆ€ {Î” Î Î¨} â†’ Î” âŠ¢ Î allvalid* â†’ Î âŠ¢ Î¨ allvalid*
                   â†’ Î” âŠ¢ Î¨ allvalid*
msubs* Î¾ Ïˆ = maps (msub Î¾) Ïˆ


--------------------------------------------------------------------------------


unlam : âˆ€ {Î” Î“ A B} â†’ Î” âŠ¢ A âŠƒ B valid[ Î“ ]
                    â†’ Î” âŠ¢ B valid[ Î“ , A ]
unlam ğ’Ÿ = app (wk ğ’Ÿ) vz


cut : âˆ€ {Î” Î“ A B} â†’ Î” âŠ¢ A valid[ Î“ ] â†’ Î” âŠ¢ B valid[ Î“ , A ]
                  â†’ Î” âŠ¢ B valid[ Î“ ]
cut ğ’Ÿ â„° = sub (ids , ğ’Ÿ) â„°


pseudocut : âˆ€ {Î” Î“ A B} â†’ Î” âŠ¢ A valid[ Î“ ] â†’ Î” âŠ¢ B valid[ Î“ , A ]
                        â†’ Î” âŠ¢ B valid[ Î“ ]
pseudocut ğ’Ÿ â„° = app (lam â„°) ğ’Ÿ


pseudosub : âˆ€ {Î” Î“ Î A} â†’ Î” âŠ¢ Î allvalid[ Î“ ] â†’ Î” âŠ¢ A valid[ Î ]
                        â†’ Î” âŠ¢ A valid[ Î“ ]
pseudosub âˆ™       ğ’Ÿ = ren botâŠ‡ ğ’Ÿ
pseudosub (Î¾ , ğ’) ğ’Ÿ = app (pseudosub Î¾ (lam ğ’Ÿ)) ğ’


exch : âˆ€ {Î” Î“ A B C} â†’ Î” âŠ¢ C valid[ Î“ , A , B ]
                     â†’ Î” âŠ¢ C valid[ Î“ , B , A ]
exch ğ’Ÿ = app (app (wk (wk (lam (lam ğ’Ÿ)))) vz) (wk vz)


--------------------------------------------------------------------------------


vau : âˆ€ {Î” Î“ A B} â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ B valid[ Î“ ]
                  â†’ Î” âŠ¢ B valid[ Î“ , â–¡ A ]
vau ğ’Ÿ = letbox vz (wk ğ’Ÿ)


unvau : âˆ€ {Î” Î“ A B} â†’ Î” âŠ¢ B valid[ Î“ , â–¡ A ]
                    â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ B valid[ Î“ ]
unvau ğ’Ÿ = app (lam (mwk ğ’Ÿ)) (box mvz)


axiomK : âˆ€ {A B Î” Î“} â†’ Î” âŠ¢ â–¡ (A âŠƒ B) valid[ Î“ ] â†’ Î” âŠ¢ â–¡ A valid[ Î“ ]
                     â†’ Î” âŠ¢ â–¡ B valid[ Î“ ]
axiomK ğ’Ÿ â„° = letbox ğ’Ÿ (letbox (mwk â„°) (box (app (mwk mvz) mvz)))


axiomT : âˆ€ {A Î” Î“} â†’ Î” âŠ¢ â–¡ A valid[ Î“ ]
                   â†’ Î” âŠ¢ A valid[ Î“ ]
axiomT ğ’Ÿ = letbox ğ’Ÿ mvz


axiom4 : âˆ€ {A Î” Î“} â†’ Î” âŠ¢ â–¡ A valid[ Î“ ]
                   â†’ Î” âŠ¢ â–¡ â–¡ A valid[ Î“ ]
axiom4 ğ’Ÿ = letbox ğ’Ÿ (box (box mvz))


mcut : âˆ€ {Î” Î“ A B} â†’ Î” âŠ¢ A valid[ âˆ™ ] â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ B valid[ Î“ ]
                   â†’ Î” âŠ¢ B valid[ Î“ ]
mcut ğ’Ÿ â„° = msub (mids* , ğ’Ÿ) â„°


pseudomcut : âˆ€ {Î” Î“ A B} â†’ Î” âŠ¢ A valid[ âˆ™ ] â†’ Î” , âŸªâŠ« A âŸ« âŠ¢ B valid[ Î“ ]
                   â†’ Î” âŠ¢ B valid[ Î“ ]
pseudomcut ğ’Ÿ â„° = letbox (box ğ’Ÿ) â„°


pseudomsub : âˆ€ {Î” Î“ Î A} â†’ Î” âŠ¢ Î allvalid* â†’ Î âŠ¢ A valid[ Î“ ]
                         â†’ Î” âŠ¢ A valid[ Î“ ]
pseudomsub âˆ™       ğ’Ÿ = mren botâŠ‡ ğ’Ÿ
pseudomsub (Î¾ , ğ’) ğ’Ÿ = app (pseudomsub Î¾ (lam (vau ğ’Ÿ))) (box ğ’)


mexch : âˆ€ {Î” Î“ A B C} â†’ Î” , âŸªâŠ« A âŸ« , âŸªâŠ« B âŸ« âŠ¢ C valid[ Î“ ]
                      â†’ Î” , âŸªâŠ« B âŸ« , âŸªâŠ« A âŸ« âŠ¢ C valid[ Î“ ]
mexch ğ’Ÿ = unvau (unvau (exch (vau (vau ğ’Ÿ))))


--------------------------------------------------------------------------------


module IPLâ†’S4
  where
    âŒˆ_âŒ‰ : IPL.Prop â†’ Prop
    âŒˆ IPL.Î¹ P âŒ‰   = Î¹ P
    âŒˆ A IPL.âŠƒ B âŒ‰ = âŒˆ A âŒ‰ âŠƒ âŒˆ B âŒ‰

    âŒˆ_âŒ‰* : List IPL.Prop â†’ List Prop
    âŒˆ Î“ âŒ‰* = map âŒˆ_âŒ‰ Î“

    â†‘âˆ‹ : âˆ€ {Î“ A} â†’ Î“ âˆ‹ A
                 â†’ âŒˆ Î“ âŒ‰* âˆ‹ âŒˆ A âŒ‰
    â†‘âˆ‹ zero    = zero
    â†‘âˆ‹ (suc i) = suc (â†‘âˆ‹ i)

    â†‘ : âˆ€ {Î” Î“ A} â†’ Î“ IPL.âŠ¢ A true
                  â†’ Î” âŠ¢ âŒˆ A âŒ‰ valid[ âŒˆ Î“ âŒ‰* ]
    â†‘ (IPL.var i)   = var (â†‘âˆ‹ i)
    â†‘ (IPL.lam ğ’Ÿ)   = lam (â†‘ ğ’Ÿ)
    â†‘ (IPL.app ğ’Ÿ â„°) = app (â†‘ ğ’Ÿ) (â†‘ â„°)


module S4â†’IPL
  where
    âŒŠ_âŒ‹ : Prop â†’ IPL.Prop
    âŒŠ Î¹ P âŒ‹   = IPL.Î¹ P
    âŒŠ A âŠƒ B âŒ‹ = âŒŠ A âŒ‹ IPL.âŠƒ âŒŠ B âŒ‹
    âŒŠ â–¡ A âŒ‹   = âŒŠ A âŒ‹

    âŒŠ_âŒ‹* : List Prop â†’ List IPL.Prop
    âŒŠ Î“ âŒ‹* = map âŒŠ_âŒ‹ Î“

    âŒŠ_âŒ‹** : List Assert â†’ List IPL.Prop
    âŒŠ Î” âŒ‹** = map (\ { âŸªâŠ« A âŸ« â†’ âŒŠ A âŒ‹ }) Î”

    â†“âˆ‹ : âˆ€ {Î“ A} â†’ Î“ âˆ‹ A
                 â†’ âŒŠ Î“ âŒ‹* âˆ‹ âŒŠ A âŒ‹
    â†“âˆ‹ zero    = zero
    â†“âˆ‹ (suc i) = suc (â†“âˆ‹ i)

    â†“âˆ‹* : âˆ€ {Î” A} â†’ Î” âˆ‹ âŸªâŠ« A âŸ«
                  â†’ âŒŠ Î” âŒ‹** âˆ‹ âŒŠ A âŒ‹
    â†“âˆ‹* zero    = zero
    â†“âˆ‹* (suc i) = suc (â†“âˆ‹* i)

    â†“ : âˆ€ {Î” Î“ A} â†’ Î” âŠ¢ A valid[ Î“ ]
                  â†’ âŒŠ Î” âŒ‹** â§º âŒŠ Î“ âŒ‹* IPL.âŠ¢ âŒŠ A âŒ‹ true
    â†“ {Î” = Î”} (var i)      = IPL.ren (ldrops âŒŠ Î” âŒ‹** id) (IPL.var (â†“âˆ‹ i))
    â†“         (lam ğ’Ÿ)      = IPL.lam (â†“ ğ’Ÿ)
    â†“         (app ğ’Ÿ â„°)    = IPL.app (â†“ ğ’Ÿ) (â†“ â„°)
    â†“ {Î“ = Î“} (mvar i)     = IPL.ren (rdrops âŒŠ Î“ âŒ‹* id) (IPL.var (â†“âˆ‹* i))
    â†“ {Î“ = Î“} (box ğ’Ÿ)      = IPL.ren (rdrops âŒŠ Î“ âŒ‹* id) (â†“ ğ’Ÿ)
    â†“ {Î“ = Î“} (letbox ğ’Ÿ â„°) = IPL.cut (â†“ ğ’Ÿ) (IPL.pull âŒŠ Î“ âŒ‹* (â†“ â„°))


--------------------------------------------------------------------------------
