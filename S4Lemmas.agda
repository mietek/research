module S4Lemmas where

open import Prelude
open import Category
open import List
open import ListLemmas
open import AllList
open import AllListLemmas
open import S4Propositions
open import S4Derivations


--------------------------------------------------------------------------------
{-
                      get (rens Î· Î¾) i â‰¡ (ren Î· âˆ˜ get Î¾) i                      comp-ren-get
                   gets (rens Î·â‚ Î¾) Î·â‚‚ â‰¡ (rens Î·â‚ âˆ˜ gets Î¾) Î·â‚‚                  comp-rens-gets
             (gets (lifts Î¾) âˆ˜ keep) Î· â‰¡ (lifts âˆ˜ gets Î¾) Î·                     comp-lifts-gets
                             get ids i â‰¡ var i                                  get-var

                              ren id ğ’Ÿ â‰¡ ğ’Ÿ                                      id-ren
                             rens id Î¾ â‰¡ Î¾                                      id-rens
                       ren (Î·â‚ âˆ˜ Î·â‚‚) ğ’Ÿ â‰¡ (ren Î·â‚‚ âˆ˜ ren Î·â‚) ğ’Ÿ                    comp-ren
                      rens (Î·â‚ âˆ˜ Î·â‚‚) Î¾ â‰¡ (rens Î·â‚‚ âˆ˜ rens Î·â‚) Î¾                  comp-rens
                                                                                ğ«ğğ§
                                                                                ğ«ğğ§ğ¬

                 (ren (keep Î·) âˆ˜ wk) ğ’Ÿ â‰¡ (wk âˆ˜ ren Î·) ğ’Ÿ                         comp-wk-ren-keep
               (rens (keep Î·) âˆ˜ wks) Î¾ â‰¡ (wks âˆ˜ rens Î·) Î¾                       comp-wks-rens-keep
             (rens (keep Î·) âˆ˜ lifts) Î¾ â‰¡ (lifts âˆ˜ rens Î·) Î¾                     comp-lifts-rens

                  (mren Î·â‚ âˆ˜ ren Î·â‚‚) ğ’Ÿ â‰¡ (ren Î·â‚‚ âˆ˜ mren Î·â‚) ğ’Ÿ                   comp-ren-mren
                (mrens Î·â‚ âˆ˜ rens Î·â‚‚) Î¾ â‰¡ (rens Î·â‚‚ âˆ˜ mrens Î·â‚) Î¾                 comp-rens-mrens
                   (mrens Î· âˆ˜ lifts) Î¾ â‰¡ (lifts âˆ˜ mrens Î·) Î¾                    comp-lifts-mrens
                           mrens Î· ids â‰¡ ids                                    id-mrens-ids

                     get (mrens Î· Î¾) i â‰¡ (mren Î· âˆ˜ get Î¾) i                     comp-mren-get
                    get (mrens* Î· Î¾) i â‰¡ (mren Î· âˆ˜ get Î¾) i                     comp-mren-get*
                  gets (mrens Î·â‚ Î¾) Î·â‚‚ â‰¡ (mrens Î·â‚ âˆ˜ gets Î¾) Î·â‚‚                 comp-mrens-gets
                 gets (mrens* Î·â‚ Î¾) Î·â‚‚ â‰¡ (mrens* Î·â‚ âˆ˜ gets Î¾) Î·â‚‚                comp-mrens*-gets
           (gets (mlifts* Î¾) âˆ˜ keep) Î· â‰¡ (mlifts* âˆ˜ gets Î¾) Î·                   comp-mlifts*-gets
                           get mids* i â‰¡ mvar i                                 get-mvar

                             mren id ğ’Ÿ â‰¡ ğ’Ÿ                                      id-mren
                            mrens id Î¾ â‰¡ Î¾                                      id-mrens
                      mren (Î·â‚ âˆ˜ Î·â‚‚) ğ’Ÿ â‰¡ (mren Î·â‚‚ âˆ˜ mren Î·â‚) ğ’Ÿ                  comp-mren
                     mrens (Î·â‚ âˆ˜ Î·â‚‚) Î¾ â‰¡ (mrens Î·â‚‚ âˆ˜ mrens Î·â‚) Î¾                comp-mrens
                                                                                ğ¦ğ«ğğ§
                                                                                ğ¦ğ«ğğ§ğ¬

               (mren (keep Î·) âˆ˜ mwk) ğ’Ÿ â‰¡ (mwk âˆ˜ mren Î·) ğ’Ÿ                       comp-mwk-mren-keep
             (mrens (keep Î·) âˆ˜ mwks) Î¾ â‰¡ (mwks âˆ˜ mrens Î·) Î¾                     comp-mwks-mrens-keep
           (mrens* (keep Î·) âˆ˜ mwks*) Î¾ â‰¡ (mwks* âˆ˜ mrens* Î·) Î¾                   comp-mwks*-mrens*-keep
         (mrens* (keep Î·) âˆ˜ mlifts*) Î¾ â‰¡ (mlifts* âˆ˜ mrens* Î·) Î¾                 comp-mlifts*-mrens*

                      get (subs Î¾ Ïˆ) i â‰¡ (sub Î¾ âˆ˜ get Ïˆ) i                      comp-sub-get
                      sub (gets Î¾ Î·) ğ’Ÿ â‰¡ (sub Î¾ âˆ˜ ren Î·) ğ’Ÿ                      comp-sub-ren
                  (sub (Î¾ , ğ’Ÿ) âˆ˜ wk) â„° â‰¡ sub Î¾ â„°                                id-cons-wk-sub
                (subs (Î¾ , ğ’Ÿ) âˆ˜ wks) Ïˆ â‰¡ subs Î¾ Ïˆ                               id-cons-wks-subs

                      sub (rens Î· Î¾) ğ’Ÿ â‰¡ (ren Î· âˆ˜ sub Î¾) ğ’Ÿ                      comp-ren-sub
                     subs (rens Î· Î¾) Ïˆ â‰¡ (rens Î· âˆ˜ subs Î¾) Ïˆ                    comp-rens-subs
            (subs (lifts Î¾) âˆ˜ lifts) Ïˆ â‰¡ (lifts âˆ˜ subs Î¾) Ïˆ                     comp-lifts-subs

          (sub (mrens Î· Î¾) âˆ˜ mren Î·) ğ’Ÿ â‰¡ (mren Î· âˆ˜ sub Î¾) ğ’Ÿ                     comp-mren-sub
        (subs (mrens Î· Î¾) âˆ˜ mrens Î·) Ïˆ â‰¡ (mrens Î· âˆ˜ subs Î¾) Ïˆ                   comp-mrens-subs

                             sub ids ğ’Ÿ â‰¡ ğ’Ÿ                                      id-sub
                      sub (subs Î¾ Ïˆ) ğ’Ÿ â‰¡ (sub Î¾ âˆ˜ sub Ïˆ) ğ’Ÿ                      comp-sub
                            subs ids Î¾ â‰¡ Î¾                                      lid-subs
                            subs Î¾ ids â‰¡ Î¾                                      rid-subs
                     subs (subs Î¾ Ïˆ) Ï† â‰¡ subs Î¾ (subs Ïˆ Ï†)                      assoc-subs
                                                                                ğ’ğŸ’
                                                                                ğ¬ğ®ğ›

                     get (msubs Î¾ Ïˆ) i â‰¡ (msub Î¾ âˆ˜ get Ïˆ) i                     comp-msub-get
                    get (msubs* Î¾ Ïˆ) i â‰¡ (msub Î¾ âˆ˜ get Ïˆ) i                     comp-msub-get*
                    (ren Î· âˆ˜ msub Î¾) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ ren Î·) ğ’Ÿ                     comp-msub-ren
                  (rens Î· âˆ˜ msubs Î¾) Ïˆ â‰¡ (msubs Î¾ âˆ˜ rens Î·) Ïˆ                   comp-msubs-rens
                   (lifts âˆ˜ msubs Î¾) Ïˆ â‰¡ (msubs Î¾ âˆ˜ lifts) Ïˆ                    comp-msubs-lifts
                     msub (gets Î¾ Î·) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ mren Î·) ğ’Ÿ                    comp-msub-mren
                (msub (Î¾ , ğ’Ÿ) âˆ˜ mwk) â„° â‰¡ msub Î¾ â„°                               id-cons-mwk-msub
              (msubs (Î¾ , ğ’Ÿ) âˆ˜ mwks) Ïˆ â‰¡ msubs Î¾ Ïˆ                              id-cons-mwks-msubs
            (msubs* (Î¾ , ğ’Ÿ) âˆ˜ mwks*) Ïˆ â‰¡ msubs* Î¾ Ïˆ                             id-cons-mwks*-msubs*

                    msub (mrens Î· Î¾) ğ’Ÿ â‰¡ (mren Î· âˆ˜ msub Î¾) ğ’Ÿ                    comp-mren-msub
                   msubs (mrens Î· Î¾) Ïˆ â‰¡ (mrens Î· âˆ˜ msubs Î¾) Ïˆ                  comp-mrens-msubs
           (msubs (mlifts Î¾) âˆ˜ mwks) Ïˆ â‰¡ (mwks âˆ˜ msubs Î¾) Ïˆ                     comp-mwks-msubs
                 msubs* (mrens* Î· Î¾) Ïˆ â‰¡ (mrens* Î· âˆ˜ msubs* Î¾) Ïˆ                comp-mrens*-msubs*
        (msubs* (mlifts* Î¾) âˆ˜ mwks*) Ïˆ â‰¡ (mwks* âˆ˜ msubs* Î¾) Ïˆ                   comp-mwks*-msubs*
      (msubs* (mlifts* Î¾) âˆ˜ mlifts*) Ïˆ â‰¡ (mlifts* âˆ˜ msubs* Î¾) Ïˆ                 comp-mlifts*-msubs*

          (sub (msubs Î¾ Ïˆ) âˆ˜ msub Î¾) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ sub Ïˆ) ğ’Ÿ                     comp-msub-sub

                          msub mids* ğ’Ÿ â‰¡ ğ’Ÿ                                      id-msub
                         msubs mids* Î¾ â‰¡ Î¾                                      id-msubs
                        msubs* mids* Î¾ â‰¡ Î¾                                      lid-msubs
                        msubs* Î¾ mids* â‰¡ Î¾                                      rid-msubs
                   msub (msubs* Î¾ Ïˆ) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ msub Ïˆ) ğ’Ÿ                    comp-msub
                  msubs (msubs* Î¾ Ïˆ) Ï† â‰¡ (msubs Î¾ âˆ˜ msubs Ïˆ) Ï†                  comp-msubs
                 msubs* (msubs* Î¾ Ïˆ) Ï† â‰¡ msubs* Î¾ (msubs* Ïˆ Ï†)                  assoc-msubs
                                                                                ğ’ğŸ’*
                                                                                ğ¦ğ¬ğ®ğ›
                                                                                ğ¦ğ¬ğ®ğ›ğ¬
-}
--------------------------------------------------------------------------------


comp-ren-get : âˆ€ {Î” Î“ Î“â€² Î A} â†’ (Î· : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (i : Î âˆ‹ A)
                              â†’ get (rens Î· Î¾) i â‰¡ (ren Î· âˆ˜ get Î¾) i
comp-ren-get Î· (Î¾ , ğ’Ÿ) zero    = refl
comp-ren-get Î· (Î¾ , â„°) (suc i) = comp-ren-get Î· Î¾ i


comp-rens-gets : âˆ€ {Î” Î“ Î“â€² Î Îâ€²} â†’ (Î·â‚ : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Îâ€² allvalid[ Î“ ]) (Î·â‚‚ : Îâ€² âŠ‡ Î)
                                 â†’ gets (rens Î·â‚ Î¾) Î·â‚‚ â‰¡ (rens Î·â‚ âˆ˜ gets Î¾) Î·â‚‚
comp-rens-gets Î·â‚ âˆ™       done      = refl
comp-rens-gets Î·â‚ (Î¾ , â„°) (drop Î·â‚‚) = comp-rens-gets Î·â‚ Î¾ Î·â‚‚
comp-rens-gets Î·â‚ (Î¾ , ğ’Ÿ) (keep Î·â‚‚) = (_, ren Î·â‚ ğ’Ÿ) & comp-rens-gets Î·â‚ Î¾ Î·â‚‚


comp-lifts-gets : âˆ€ {Î” Î“ Î Îâ€² A} â†’ (Î¾ : Î” âŠ¢ Îâ€² allvalid[ Î“ ]) (Î· : Îâ€² âŠ‡ Î)
                                 â†’ (gets (lifts {A} Î¾) âˆ˜ keep) Î· â‰¡ (lifts âˆ˜ gets Î¾) Î·
comp-lifts-gets Î¾ Î· = (_, vz) & comp-rens-gets (drop id) Î¾ Î·


get-var : âˆ€ {Î” Î“ A} â†’ (i : Î“ âˆ‹ A)
                    â†’ get (ids {Î” = Î”}) i â‰¡ var i
get-var zero    = refl
get-var (suc i) = comp-ren-get (drop id) ids i
                â‹® wk & get-var i
                â‹® (\ iâ€² â†’ var (suc iâ€²)) & id-renâˆ‹ i


--------------------------------------------------------------------------------


id-ren : âˆ€ {Î” Î“ A} â†’ (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                   â†’ ren id ğ’Ÿ â‰¡ ğ’Ÿ
id-ren (var i)      = var & id-renâˆ‹ i
id-ren (lam ğ’Ÿ)      = lam & id-ren ğ’Ÿ
id-ren (app ğ’Ÿ â„°)    = app & id-ren ğ’Ÿ âŠ— id-ren â„°
id-ren (mvar i)     = refl
id-ren (box ğ’Ÿ)      = refl
id-ren (letbox ğ’Ÿ â„°) = letbox & id-ren ğ’Ÿ âŠ— id-ren â„°


id-rens : âˆ€ {Î” Î“ Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                    â†’ rens id Î¾ â‰¡ Î¾
id-rens âˆ™       = refl
id-rens (Î¾ , ğ’Ÿ) = _,_ & id-rens Î¾ âŠ— id-ren ğ’Ÿ


comp-ren : âˆ€ {Î” Î“ Î“â€² Î“â€³ A} â†’ (Î·â‚ : Î“â€² âŠ‡ Î“) (Î·â‚‚ : Î“â€³ âŠ‡ Î“â€²) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                           â†’ ren (Î·â‚ âˆ˜ Î·â‚‚) ğ’Ÿ â‰¡ (ren Î·â‚‚ âˆ˜ ren Î·â‚) ğ’Ÿ
comp-ren Î·â‚ Î·â‚‚ (var i)      = var & comp-renâˆ‹ Î·â‚ Î·â‚‚ i
comp-ren Î·â‚ Î·â‚‚ (lam ğ’Ÿ)      = lam & comp-ren (keep Î·â‚) (keep Î·â‚‚) ğ’Ÿ
comp-ren Î·â‚ Î·â‚‚ (app ğ’Ÿ â„°)    = app & comp-ren Î·â‚ Î·â‚‚ ğ’Ÿ âŠ— comp-ren Î·â‚ Î·â‚‚ â„°
comp-ren Î·â‚ Î·â‚‚ (mvar i)     = refl
comp-ren Î·â‚ Î·â‚‚ (box ğ’Ÿ)      = refl
comp-ren Î·â‚ Î·â‚‚ (letbox ğ’Ÿ â„°) = letbox & comp-ren Î·â‚ Î·â‚‚ ğ’Ÿ âŠ— comp-ren Î·â‚ Î·â‚‚ â„°


comp-rens : âˆ€ {Î” Î“ Î“â€² Î“â€³ Î} â†’ (Î·â‚ : Î“â€² âŠ‡ Î“) (Î·â‚‚ : Î“â€³ âŠ‡ Î“â€²) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                            â†’ rens (Î·â‚ âˆ˜ Î·â‚‚) Î¾ â‰¡ (rens Î·â‚‚ âˆ˜ rens Î·â‚) Î¾
comp-rens Î·â‚ Î·â‚‚ âˆ™       = refl
comp-rens Î·â‚ Î·â‚‚ (Î¾ , ğ’Ÿ) = _,_ & comp-rens Î·â‚ Î·â‚‚ Î¾ âŠ— comp-ren Î·â‚ Î·â‚‚ ğ’Ÿ


ğ«ğğ§ : âˆ€ {A} â†’ Presheaf ğğğ„ (\ Î“ â†’ Î£ (List Assert) (\ Î” â†’ Î” âŠ¢ A valid[ Î“ ]))
ğ«ğğ§ = record
        { â„±     = \ { Î· (Î” , ğ’Ÿ) â†’ Î” , ren Î· ğ’Ÿ }
        ; idâ„±   = funext! (\ { (Î” , ğ’Ÿ) â†’ (Î” ,_) & id-ren ğ’Ÿ })
        ; compâ„± = \ Î·â‚ Î·â‚‚ â†’ funext! (\ { (Î” , ğ’Ÿ) â†’ (Î” ,_) & comp-ren Î·â‚ Î·â‚‚ ğ’Ÿ })
        }


ğ«ğğ§ğ¬ : âˆ€ {Î} â†’ Presheaf ğğğ„ (\ Î“ â†’ Î£ (List Assert) (\ Î” â†’ Î” âŠ¢ Î allvalid[ Î“ ]))
ğ«ğğ§ğ¬ = record
         { â„±     = \ { Î· (Î” , Î¾) â†’ Î” , rens Î· Î¾ }
         ; idâ„±   = funext! (\ { (Î” , Î¾) â†’ (Î” ,_) & id-rens Î¾ })
         ; compâ„± = \ Î·â‚ Î·â‚‚ â†’ funext! (\ { (Î” , Î¾) â†’ (Î” ,_) & comp-rens Î·â‚ Î·â‚‚ Î¾ })
         }


--------------------------------------------------------------------------------


comp-wk-ren-keep : âˆ€ {Î” Î“ Î“â€² A B} â†’ (Î· : Î“â€² âŠ‡ Î“) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                                  â†’ (ren (keep {A = B} Î·) âˆ˜ wk) ğ’Ÿ â‰¡ (wk âˆ˜ ren Î·) ğ’Ÿ
comp-wk-ren-keep Î· ğ’Ÿ = comp-ren (drop id) (keep Î·) ğ’Ÿ â»Â¹
                     â‹® (\ Î·â€² â†’ ren (drop Î·â€²) ğ’Ÿ) & (lidâˆ˜ Î· â‹® ridâˆ˜ Î· â»Â¹)
                     â‹® comp-ren Î· (drop id) ğ’Ÿ


comp-wks-rens-keep : âˆ€ {Î” Î“ Î“â€² Î A} â†’ (Î· : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                                    â†’ (rens (keep {A = A} Î·) âˆ˜ wks) Î¾ â‰¡ (wks âˆ˜ rens Î·) Î¾
comp-wks-rens-keep Î· âˆ™       = refl
comp-wks-rens-keep Î· (Î¾ , ğ’Ÿ) = _,_ & comp-wks-rens-keep Î· Î¾ âŠ— comp-wk-ren-keep Î· ğ’Ÿ


comp-lifts-rens : âˆ€ {Î” Î“ Î“â€² Î A} â†’ (Î· : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                                 â†’ (rens (keep {A = A} Î·) âˆ˜ lifts) Î¾ â‰¡ (lifts âˆ˜ rens Î·) Î¾
comp-lifts-rens Î· Î¾ = (_, vz) & comp-wks-rens-keep Î· Î¾


--------------------------------------------------------------------------------


comp-ren-mren : âˆ€ {Î” Î”â€² Î“ Î“â€² A} â†’ (Î·â‚ : Î”â€² âŠ‡ Î”) (Î·â‚‚ : Î“â€² âŠ‡ Î“) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                                â†’ (mren Î·â‚ âˆ˜ ren Î·â‚‚) ğ’Ÿ â‰¡ (ren Î·â‚‚ âˆ˜ mren Î·â‚) ğ’Ÿ
comp-ren-mren Î·â‚ Î·â‚‚ (var i)      = refl
comp-ren-mren Î·â‚ Î·â‚‚ (lam ğ’Ÿ)      = lam & comp-ren-mren Î·â‚ (keep Î·â‚‚) ğ’Ÿ
comp-ren-mren Î·â‚ Î·â‚‚ (app ğ’Ÿ â„°)    = app & comp-ren-mren Î·â‚ Î·â‚‚ ğ’Ÿ âŠ— comp-ren-mren Î·â‚ Î·â‚‚ â„°
comp-ren-mren Î·â‚ Î·â‚‚ (mvar i)     = refl
comp-ren-mren Î·â‚ Î·â‚‚ (box ğ’Ÿ)      = refl
comp-ren-mren Î·â‚ Î·â‚‚ (letbox ğ’Ÿ â„°) = letbox & comp-ren-mren Î·â‚ Î·â‚‚ ğ’Ÿ âŠ— comp-ren-mren (keep Î·â‚) Î·â‚‚ â„°


comp-rens-mrens : âˆ€ {Î” Î”â€² Î“ Î“â€² Î} â†’ (Î·â‚ : Î”â€² âŠ‡ Î”) (Î·â‚‚ : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                                  â†’ (mrens Î·â‚ âˆ˜ rens Î·â‚‚) Î¾ â‰¡ (rens Î·â‚‚ âˆ˜ mrens Î·â‚) Î¾
comp-rens-mrens Î·â‚ Î·â‚‚ âˆ™       = refl
comp-rens-mrens Î·â‚ Î·â‚‚ (Î¾ , ğ’Ÿ) = _,_ & comp-rens-mrens Î·â‚ Î·â‚‚ Î¾ âŠ— comp-ren-mren Î·â‚ Î·â‚‚ ğ’Ÿ


comp-lifts-mrens : âˆ€ {Î” Î”â€² Î“ Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                                  â†’ (mrens Î· âˆ˜ lifts {A}) Î¾ â‰¡ (lifts âˆ˜ mrens Î·) Î¾
comp-lifts-mrens Î· Î¾ = (_, vz) & comp-rens-mrens Î· (drop id) Î¾


id-mrens-ids : âˆ€ {Î” Î”â€² Î“} â†’ (Î· : Î”â€² âŠ‡ Î”)
                          â†’ mrens Î· (ids {Î“ = Î“}) â‰¡ ids
id-mrens-ids {Î“ = âˆ™}     Î· = refl
id-mrens-ids {Î“ = Î“ , A} Î· = comp-lifts-mrens Î· ids
                           â‹® lifts & id-mrens-ids Î·


--------------------------------------------------------------------------------


comp-mren-get : âˆ€ {Î” Î”â€² Î“ Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (i : Î âˆ‹ A)
                               â†’ get (mrens Î· Î¾) i â‰¡ (mren Î· âˆ˜ get Î¾) i
comp-mren-get Î· (Î¾ , ğ’Ÿ) zero    = refl
comp-mren-get Î· (Î¾ , â„°) (suc i) = comp-mren-get Î· Î¾ i


comp-mren-get* : âˆ€ {Î” Î”â€² Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid*) (i : Î âˆ‹ âŸªâŠ« A âŸ«)
                              â†’ get (mrens* Î· Î¾) i â‰¡ (mren Î· âˆ˜ get Î¾) i
comp-mren-get* Î· (Î¾ , ğ’Ÿ) zero    = refl
comp-mren-get* Î· (Î¾ , â„°) (suc i) = comp-mren-get* Î· Î¾ i


comp-mrens-gets : âˆ€ {Î” Î”â€² Î“ Î Îâ€²} â†’ (Î·â‚ : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Îâ€² allvalid[ Î“ ]) (Î·â‚‚ : Îâ€² âŠ‡ Î)
                                  â†’ gets (mrens Î·â‚ Î¾) Î·â‚‚ â‰¡ (mrens Î·â‚ âˆ˜ gets Î¾) Î·â‚‚
comp-mrens-gets Î·â‚ âˆ™       done      = refl
comp-mrens-gets Î·â‚ (Î¾ , â„°) (drop Î·â‚‚) = comp-mrens-gets Î·â‚ Î¾ Î·â‚‚
comp-mrens-gets Î·â‚ (Î¾ , ğ’Ÿ) (keep Î·â‚‚) = (_, mren Î·â‚ ğ’Ÿ) & comp-mrens-gets Î·â‚ Î¾ Î·â‚‚


comp-mrens*-gets : âˆ€ {Î” Î”â€² Î Îâ€²} â†’ (Î·â‚ : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Îâ€² allvalid*) (Î·â‚‚ : Îâ€² âŠ‡ Î)
                                 â†’ gets (mrens* Î·â‚ Î¾) Î·â‚‚ â‰¡ (mrens* Î·â‚ âˆ˜ gets Î¾) Î·â‚‚
comp-mrens*-gets Î·â‚ âˆ™       done      = refl
comp-mrens*-gets Î·â‚ (Î¾ , â„°) (drop Î·â‚‚) = comp-mrens*-gets Î·â‚ Î¾ Î·â‚‚
comp-mrens*-gets Î·â‚ (Î¾ , ğ’Ÿ) (keep Î·â‚‚) = (_, mren Î·â‚ ğ’Ÿ) & comp-mrens*-gets Î·â‚ Î¾ Î·â‚‚


comp-mlifts*-gets : âˆ€ {Î” Î Îâ€² A} â†’ (Î¾ : Î” âŠ¢ Îâ€² allvalid*) (Î· : Îâ€² âŠ‡ Î)
                                 â†’ (gets (mlifts* {A} Î¾) âˆ˜ keep) Î· â‰¡ (mlifts* âˆ˜ gets Î¾) Î·
comp-mlifts*-gets Î¾ Î· = (_, mvz) & comp-mrens*-gets (drop id) Î¾ Î·


get-mvar : âˆ€ {Î” A} â†’ (i : Î” âˆ‹ A)
                   â†’ get mids* i â‰¡ mvar i
get-mvar zero    = refl
get-mvar (suc i) = comp-mren-get* (drop id) mids* i
                 â‹® mwk & get-mvar i
                 â‹® (\ iâ€² â†’ mvar (suc iâ€²)) & id-renâˆ‹ i


--------------------------------------------------------------------------------


id-mren : âˆ€ {Î” Î“ A} â†’ (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                    â†’ mren id ğ’Ÿ â‰¡ ğ’Ÿ
id-mren (var i)      = refl
id-mren (lam ğ’Ÿ)      = lam & id-mren ğ’Ÿ
id-mren (app ğ’Ÿ â„°)    = app & id-mren ğ’Ÿ âŠ— id-mren â„°
id-mren (mvar i)     = mvar & id-renâˆ‹ i
id-mren (box ğ’Ÿ)      = box & id-mren ğ’Ÿ
id-mren (letbox ğ’Ÿ â„°) = letbox & id-mren ğ’Ÿ âŠ— id-mren â„°


id-mrens : âˆ€ {Î” Î“ Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                     â†’ mrens id Î¾ â‰¡ Î¾
id-mrens âˆ™       = refl
id-mrens (Î¾ , ğ’Ÿ) = _,_ & id-mrens Î¾ âŠ— id-mren ğ’Ÿ


comp-mren : âˆ€ {Î” Î”â€² Î”â€³ Î“ A} â†’ (Î·â‚ : Î”â€² âŠ‡ Î”) (Î·â‚‚ : Î”â€³ âŠ‡ Î”â€²) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                            â†’ mren (Î·â‚ âˆ˜ Î·â‚‚) ğ’Ÿ â‰¡ (mren Î·â‚‚ âˆ˜ mren Î·â‚) ğ’Ÿ
comp-mren Î·â‚ Î·â‚‚ (var i)      = refl
comp-mren Î·â‚ Î·â‚‚ (lam ğ’Ÿ)      = lam & comp-mren Î·â‚ Î·â‚‚ ğ’Ÿ
comp-mren Î·â‚ Î·â‚‚ (app ğ’Ÿ â„°)    = app & comp-mren Î·â‚ Î·â‚‚ ğ’Ÿ âŠ— comp-mren Î·â‚ Î·â‚‚ â„°
comp-mren Î·â‚ Î·â‚‚ (mvar i)     = mvar & comp-renâˆ‹ Î·â‚ Î·â‚‚ i
comp-mren Î·â‚ Î·â‚‚ (box ğ’Ÿ)      = box & comp-mren Î·â‚ Î·â‚‚ ğ’Ÿ
comp-mren Î·â‚ Î·â‚‚ (letbox ğ’Ÿ â„°) = letbox & comp-mren Î·â‚ Î·â‚‚ ğ’Ÿ âŠ— comp-mren (keep Î·â‚) (keep Î·â‚‚) â„°


comp-mrens : âˆ€ {Î” Î”â€² Î”â€³ Î“ Î} â†’ (Î·â‚ : Î”â€² âŠ‡ Î”) (Î·â‚‚ : Î”â€³ âŠ‡ Î”â€²) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                             â†’ mrens (Î·â‚ âˆ˜ Î·â‚‚) Î¾ â‰¡ (mrens Î·â‚‚ âˆ˜ mrens Î·â‚) Î¾
comp-mrens Î·â‚ Î·â‚‚ âˆ™       = refl
comp-mrens Î·â‚ Î·â‚‚ (Î¾ , ğ’Ÿ) = _,_ & comp-mrens Î·â‚ Î·â‚‚ Î¾ âŠ— comp-mren Î·â‚ Î·â‚‚ ğ’Ÿ


ğ¦ğ«ğğ§ : âˆ€ {A} â†’ Presheaf ğğğ„ (\ Î” â†’ Î£ (List Prop) (\ Î“ â†’ Î” âŠ¢ A valid[ Î“ ]))
ğ¦ğ«ğğ§ = record
         { â„±     = \ { Î· (Î“ , ğ’Ÿ) â†’ Î“ , mren Î· ğ’Ÿ }
         ; idâ„±   = funext! (\ { (Î“ , ğ’Ÿ) â†’ (Î“ ,_) & id-mren ğ’Ÿ })
         ; compâ„± = \ Î·â‚ Î·â‚‚ â†’ funext! (\ { (Î“ , ğ’Ÿ) â†’ (Î“ ,_) & comp-mren Î·â‚ Î·â‚‚ ğ’Ÿ })
         }


ğ¦ğ«ğğ§ğ¬ : âˆ€ {Î} â†’ Presheaf ğğğ„ (\ Î” â†’ Î£ (List Prop) (\ Î“ â†’ Î” âŠ¢ Î allvalid[ Î“ ]))
ğ¦ğ«ğğ§ğ¬ = record
          { â„±     = \ { Î· (Î“ , Î¾) â†’ Î“ , mrens Î· Î¾}
          ; idâ„±   = funext! (\ { (Î“ , Î¾) â†’ (Î“ ,_) & id-mrens Î¾ })
          ; compâ„± = \ Î·â‚ Î·â‚‚ â†’ funext! (\ { (Î“ , Î¾) â†’ (Î“ ,_) & comp-mrens Î·â‚ Î·â‚‚ Î¾ })
          }


--------------------------------------------------------------------------------


comp-mwk-mren-keep : âˆ€ {Î” Î”â€² Î“ A B} â†’ (Î· : Î”â€² âŠ‡ Î”) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                                    â†’ (mren (keep {A = B} Î·) âˆ˜ mwk) ğ’Ÿ â‰¡ (mwk âˆ˜ mren Î·) ğ’Ÿ
comp-mwk-mren-keep Î· ğ’Ÿ = comp-mren (drop id) (keep Î·) ğ’Ÿ â»Â¹
                       â‹® (\ Î·â€² â†’ mren (drop Î·â€²) ğ’Ÿ) & (lidâˆ˜ Î· â‹® ridâˆ˜ Î· â»Â¹)
                       â‹® comp-mren Î· (drop id) ğ’Ÿ


comp-mwks-mrens-keep : âˆ€ {Î” Î”â€² Î“ Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                                      â†’ (mrens (keep {A = A} Î·) âˆ˜ mwks) Î¾ â‰¡ (mwks âˆ˜ mrens Î·) Î¾
comp-mwks-mrens-keep Î· âˆ™       = refl
comp-mwks-mrens-keep Î· (Î¾ , ğ’Ÿ) = _,_ & comp-mwks-mrens-keep Î· Î¾ âŠ— comp-mwk-mren-keep Î· ğ’Ÿ


comp-mwks*-mrens*-keep : âˆ€ {Î” Î”â€² Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid*)
                                      â†’ (mrens* (keep {A = A} Î·) âˆ˜ mwks*) Î¾ â‰¡ (mwks* âˆ˜ mrens* Î·) Î¾
comp-mwks*-mrens*-keep Î· âˆ™       = refl
comp-mwks*-mrens*-keep Î· (Î¾ , ğ’Ÿ) = _,_ & comp-mwks*-mrens*-keep Î· Î¾ âŠ— comp-mwk-mren-keep Î· ğ’Ÿ


comp-mlifts*-mrens* : âˆ€ {Î” Î”â€² Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid*)
                                   â†’ (mrens* (keep {A = A} Î·) âˆ˜ mlifts*) Î¾ â‰¡ (mlifts* âˆ˜ mrens* Î·) Î¾
comp-mlifts*-mrens* Î· Î¾ = (_, mvz) & comp-mwks*-mrens*-keep Î· Î¾


--------------------------------------------------------------------------------


comp-sub-get : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ]) (i : Î¨ âˆ‹ A)
                             â†’ get (subs Î¾ Ïˆ) i â‰¡ (sub Î¾ âˆ˜ get Ïˆ) i
comp-sub-get Î¾ (Ïˆ , ğ’Ÿ) zero    = refl
comp-sub-get Î¾ (Ïˆ , â„°) (suc i) = comp-sub-get Î¾ Ïˆ i


comp-sub-ren : âˆ€ {Î” Î“ Î Îâ€² A} â†’ (Î¾ : Î” âŠ¢ Îâ€² allvalid[ Î“ ]) (Î· : Îâ€² âŠ‡ Î) (ğ’Ÿ : Î” âŠ¢ A valid[ Î ])
                              â†’ sub (gets Î¾ Î·) ğ’Ÿ â‰¡ (sub Î¾ âˆ˜ ren Î·) ğ’Ÿ
comp-sub-ren Î¾ Î· (var i)      = comp-get-renâˆ‹ Î¾ Î· i
comp-sub-ren Î¾ Î· (lam ğ’Ÿ)      = lam & ( (\ Î¾â€² â†’ sub Î¾â€² ğ’Ÿ) & comp-lifts-gets Î¾ Î· â»Â¹
                                      â‹® comp-sub-ren (lifts Î¾) (keep Î·) ğ’Ÿ
                                      )
comp-sub-ren Î¾ Î· (app ğ’Ÿ â„°)    = app & comp-sub-ren Î¾ Î· ğ’Ÿ âŠ— comp-sub-ren Î¾ Î· â„°
comp-sub-ren Î¾ Î· (mvar i)     = refl
comp-sub-ren Î¾ Î· (box ğ’Ÿ)      = refl
comp-sub-ren Î¾ Î· (letbox ğ’Ÿ â„°) = letbox & comp-sub-ren Î¾ Î· ğ’Ÿ
                                       âŠ— ( (\ Î¾â€² â†’ sub Î¾â€² â„°) & comp-mrens-gets (drop id) Î¾ Î· â»Â¹
                                         â‹® comp-sub-ren (mwks Î¾) Î· â„°
                                         )


id-cons-wk-sub : âˆ€ {Î” Î“ Î A B} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ]) (â„° : Î” âŠ¢ B valid[ Î ])
                               â†’ sub (Î¾ , ğ’Ÿ) (wk â„°) â‰¡ sub Î¾ â„°
id-cons-wk-sub Î¾ ğ’Ÿ â„° = comp-sub-ren (Î¾ , ğ’Ÿ) (drop id) â„° â»Â¹
                     â‹® (\ Î¾â€² â†’ sub Î¾â€² â„°) & id-gets Î¾


id-cons-wks-subs : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ])
                                 â†’ subs (Î¾ , ğ’Ÿ) (wks Ïˆ) â‰¡ subs Î¾ Ïˆ
id-cons-wks-subs Î¾ ğ’Ÿ âˆ™       = refl
id-cons-wks-subs Î¾ ğ’Ÿ (Ïˆ , â„°) = _,_ & id-cons-wks-subs Î¾ ğ’Ÿ Ïˆ âŠ— id-cons-wk-sub Î¾ ğ’Ÿ â„°


--------------------------------------------------------------------------------


comp-ren-sub : âˆ€ {Î” Î“ Î“â€² Î A} â†’ (Î· : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (ğ’Ÿ : Î” âŠ¢ A valid[ Î ])
                              â†’ sub (rens Î· Î¾) ğ’Ÿ â‰¡ (ren Î· âˆ˜ sub Î¾) ğ’Ÿ
comp-ren-sub Î· Î¾ (var i)      = comp-ren-get Î· Î¾ i
comp-ren-sub Î· Î¾ (lam ğ’Ÿ)      = lam & ( (\ Î¾â€² â†’ sub Î¾â€² ğ’Ÿ) & comp-lifts-rens Î· Î¾ â»Â¹
                                      â‹® comp-ren-sub (keep Î·) (lifts Î¾) ğ’Ÿ
                                      )
comp-ren-sub Î· Î¾ (app ğ’Ÿ â„°)    = app & comp-ren-sub Î· Î¾ ğ’Ÿ âŠ— comp-ren-sub Î· Î¾ â„°
comp-ren-sub Î· Î¾ (mvar i)     = refl
comp-ren-sub Î· Î¾ (box ğ’Ÿ)      = refl
comp-ren-sub Î· Î¾ (letbox ğ’Ÿ â„°) = letbox & comp-ren-sub Î· Î¾ ğ’Ÿ
                                       âŠ— ( (\ Î¾â€² â†’ sub Î¾â€² â„°) & comp-rens-mrens (drop id) Î· Î¾
                                         â‹® comp-ren-sub Î· (mwks Î¾) â„°
                                         )


comp-rens-subs : âˆ€ {Î” Î“ Î“â€² Î Î¨} â†’ (Î· : Î“â€² âŠ‡ Î“) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ])
                                â†’ subs (rens Î· Î¾) Ïˆ â‰¡ (rens Î· âˆ˜ subs Î¾) Ïˆ
comp-rens-subs Î· Î¾ âˆ™       = refl
comp-rens-subs Î· Î¾ (Ïˆ , ğ’Ÿ) = _,_ & comp-rens-subs Î· Î¾ Ïˆ âŠ— comp-ren-sub Î· Î¾ ğ’Ÿ


comp-lifts-subs : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ])
                                â†’ (subs (lifts {A} Î¾) âˆ˜ lifts) Ïˆ â‰¡ (lifts âˆ˜ subs Î¾) Ïˆ
comp-lifts-subs Î¾ Ïˆ = (_, vz) & ( id-cons-wks-subs (wks Î¾) vz Ïˆ
                                â‹® comp-rens-subs (drop id) Î¾ Ïˆ
                                )


--------------------------------------------------------------------------------


comp-mren-sub : âˆ€ {Î” Î”â€² Î“ Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (ğ’Ÿ : Î” âŠ¢ A valid[ Î ])
                               â†’ (sub (mrens Î· Î¾) âˆ˜ mren Î·) ğ’Ÿ â‰¡ (mren Î· âˆ˜ sub Î¾) ğ’Ÿ
comp-mren-sub Î· Î¾ (var i)      = comp-mren-get Î· Î¾ i
comp-mren-sub Î· Î¾ (lam ğ’Ÿ)      = lam & ( (\ Î¾â€² â†’ sub Î¾â€² (mren Î· ğ’Ÿ)) & comp-lifts-mrens Î· Î¾ â»Â¹
                                       â‹® comp-mren-sub Î· (lifts Î¾) ğ’Ÿ
                                       )
comp-mren-sub Î· Î¾ (app ğ’Ÿ â„°)    = app & comp-mren-sub Î· Î¾ ğ’Ÿ âŠ— comp-mren-sub Î· Î¾ â„°
comp-mren-sub Î· Î¾ (mvar i)     = refl
comp-mren-sub Î· Î¾ (box ğ’Ÿ)      = refl
comp-mren-sub Î· Î¾ (letbox ğ’Ÿ â„°) = letbox & comp-mren-sub Î· Î¾ ğ’Ÿ
                                        âŠ— ( (\ Î¾â€² â†’ sub Î¾â€² (mren (keep Î·) â„°)) & comp-mwks-mrens-keep Î· Î¾ â»Â¹
                                          â‹® comp-mren-sub (keep Î·) (mwks Î¾) â„°
                                          )


comp-mrens-subs : âˆ€ {Î” Î”â€² Î“ Î Î¨} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ])
                                 â†’ (subs (mrens Î· Î¾) âˆ˜ mrens Î·) Ïˆ â‰¡ (mrens Î· âˆ˜ subs Î¾) Ïˆ
comp-mrens-subs Î· Î¾ âˆ™       = refl
comp-mrens-subs Î· Î¾ (Ïˆ , ğ’Ÿ) = _,_ & comp-mrens-subs Î· Î¾ Ïˆ âŠ— comp-mren-sub Î· Î¾ ğ’Ÿ


--------------------------------------------------------------------------------


id-sub : âˆ€ {Î” Î“ A} â†’ (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                   â†’ sub ids ğ’Ÿ â‰¡ ğ’Ÿ
id-sub (var i)      = get-var i
id-sub (lam ğ’Ÿ)      = lam & id-sub ğ’Ÿ
id-sub (app ğ’Ÿ â„°)    = app & id-sub ğ’Ÿ âŠ— id-sub â„°
id-sub (mvar i)     = refl
id-sub (box ğ’Ÿ)      = refl
id-sub (letbox ğ’Ÿ â„°) = letbox & id-sub ğ’Ÿ âŠ— ( (\ Î¾â€² â†’ sub Î¾â€² â„°) & id-mrens-ids (drop id)
                                          â‹® id-sub â„°
                                          )


comp-sub : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ]) (ğ’Ÿ : Î” âŠ¢ A valid[ Î¨ ])
                         â†’ sub (subs Î¾ Ïˆ) ğ’Ÿ â‰¡ (sub Î¾ âˆ˜ sub Ïˆ) ğ’Ÿ
comp-sub Î¾ Ïˆ (var i)      = comp-sub-get Î¾ Ïˆ i
comp-sub Î¾ Ïˆ (lam ğ’Ÿ)      = lam & ( (\ Î¾â€² â†’ sub Î¾â€² ğ’Ÿ) & comp-lifts-subs Î¾ Ïˆ â»Â¹
                                  â‹® comp-sub (lifts Î¾) (lifts Ïˆ) ğ’Ÿ
                                  )
comp-sub Î¾ Ïˆ (app ğ’Ÿ â„°)    = app & comp-sub Î¾ Ïˆ ğ’Ÿ âŠ— comp-sub Î¾ Ïˆ â„°
comp-sub Î¾ Ïˆ (mvar i)     = refl
comp-sub Î¾ Ïˆ (box ğ’Ÿ)      = refl
comp-sub Î¾ Ïˆ (letbox ğ’Ÿ â„°) = letbox & comp-sub Î¾ Ïˆ ğ’Ÿ
                                   âŠ— ( (\ Î¾â€² â†’ sub Î¾â€² â„°) & comp-mrens-subs (drop id) Î¾ Ïˆ â»Â¹
                                     â‹® comp-sub (mwks Î¾) (mwks Ïˆ) â„°
                                     )


lid-subs : âˆ€ {Î” Î“ Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                     â†’ subs ids Î¾ â‰¡ Î¾
lid-subs âˆ™       = refl
lid-subs (Î¾ , ğ’Ÿ) = _,_ & lid-subs Î¾ âŠ— id-sub ğ’Ÿ


rid-subs : âˆ€ {Î” Î“ Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                     â†’ subs Î¾ ids â‰¡ Î¾
rid-subs âˆ™       = refl
rid-subs (Î¾ , ğ’Ÿ) = (_, ğ’Ÿ) & ( id-cons-wks-subs Î¾ ğ’Ÿ ids
                            â‹® rid-subs Î¾
                            )


assoc-subs : âˆ€ {Î” Î“ Î Î¨ Î¦} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ]) (Ïˆ : Î” âŠ¢ Î¨ allvalid[ Î ]) (Ï† : Î” âŠ¢ Î¦ allvalid[ Î¨ ])
                           â†’ subs (subs Î¾ Ïˆ) Ï† â‰¡ subs Î¾ (subs Ïˆ Ï†)
assoc-subs Î¾ Ïˆ âˆ™       = refl
assoc-subs Î¾ Ïˆ (Ï† , ğ’Ÿ) = _,_ & assoc-subs Î¾ Ïˆ Ï† âŠ— comp-sub Î¾ Ïˆ ğ’Ÿ


instance
  ğ’ğŸ’ : âˆ€ {Î”} â†’ Category (List Prop) (\ Î“ Î â†’ Î” âŠ¢ Î allvalid[ Î“ ])
  ğ’ğŸ’ = record
         { id     = ids
         ; _âˆ˜_    = flip subs
         ; lidâˆ˜   = rid-subs
         ; ridâˆ˜   = lid-subs
         ; assocâˆ˜ = \ Ï† Ïˆ Î¾ â†’ assoc-subs Î¾ Ïˆ Ï† â»Â¹
         }


ğ¬ğ®ğ› : âˆ€ {Î” A} â†’ Presheaf (ğ’ğŸ’ {Î”}) (\ Î“ â†’ Î” âŠ¢ A valid[ Î“ ])
ğ¬ğ®ğ› = record
        { â„±     = sub
        ; idâ„±   = funext! id-sub
        ; compâ„± = \ Ïˆ Î¾ â†’ funext! (comp-sub Î¾ Ïˆ)
        }


--------------------------------------------------------------------------------


comp-msub-get : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ]) (i : Î¨ âˆ‹ A)
                              â†’ get (msubs Î¾ Ïˆ) i â‰¡ (msub Î¾ âˆ˜ get Ïˆ) i
comp-msub-get Î¾ (Ïˆ , ğ’Ÿ) zero    = refl
comp-msub-get Î¾ (Ïˆ , â„°) (suc i) = comp-msub-get Î¾ Ïˆ i


comp-msub-get* : âˆ€ {Î” Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*) (i : Î¨ âˆ‹ âŸªâŠ« A âŸ«)
                             â†’ get (msubs* Î¾ Ïˆ) i â‰¡ (msub Î¾ âˆ˜ get Ïˆ) i
comp-msub-get* Î¾ (Ïˆ , ğ’Ÿ) zero    = refl
comp-msub-get* Î¾ (Ïˆ , â„°) (suc i) = comp-msub-get* Î¾ Ïˆ i


comp-msub-ren : âˆ€ {Î” Î“ Î“â€² Î A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Î· : Î“â€² âŠ‡ Î“) (ğ’Ÿ : Î âŠ¢ A valid[ Î“ ])
                               â†’ (ren Î· âˆ˜ msub Î¾) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ ren Î·) ğ’Ÿ
comp-msub-ren Î¾ Î· (var i)      = refl
comp-msub-ren Î¾ Î· (lam ğ’Ÿ)      = lam & comp-msub-ren Î¾ (keep Î·) ğ’Ÿ
comp-msub-ren Î¾ Î· (app ğ’Ÿ â„°)    = app & comp-msub-ren Î¾ Î· ğ’Ÿ âŠ— comp-msub-ren Î¾ Î· â„°
comp-msub-ren Î¾ Î· (mvar i)     = comp-ren-sub Î· âˆ™ (get Î¾ i) â»Â¹
comp-msub-ren Î¾ Î· (box ğ’Ÿ)      = refl
comp-msub-ren Î¾ Î· (letbox ğ’Ÿ â„°) = letbox & comp-msub-ren Î¾ Î· ğ’Ÿ âŠ— comp-msub-ren (mlifts* Î¾) Î· â„°


comp-msubs-rens : âˆ€ {Î” Î“ Î“â€² Î Î¨} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Î· : Î“â€² âŠ‡ Î“) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ])
                                 â†’ (rens Î· âˆ˜ msubs Î¾) Ïˆ â‰¡ (msubs Î¾ âˆ˜ rens Î·) Ïˆ
comp-msubs-rens Î¾ Î· âˆ™       = refl
comp-msubs-rens Î¾ Î· (Ïˆ , ğ’Ÿ) = _,_ & comp-msubs-rens Î¾ Î· Ïˆ âŠ— comp-msub-ren Î¾ Î· ğ’Ÿ


comp-msubs-lifts : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ])
                                 â†’ (lifts {A} âˆ˜ msubs Î¾) Ïˆ â‰¡ (msubs Î¾ âˆ˜ lifts) Ïˆ
comp-msubs-lifts Î¾ Ïˆ = (_, vz) & comp-msubs-rens Î¾ (drop id) Ïˆ


comp-msub-mren : âˆ€ {Î” Î“ Î Îâ€² A} â†’ (Î¾ : Î” âŠ¢ Îâ€² allvalid*) (Î· : Îâ€² âŠ‡ Î) (ğ’Ÿ : Î âŠ¢ A valid[ Î“ ])
                                â†’ msub (gets Î¾ Î·) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ mren Î·) ğ’Ÿ
comp-msub-mren Î¾ Î· (var i)      = refl
comp-msub-mren Î¾ Î· (lam ğ’Ÿ)      = lam & comp-msub-mren Î¾ Î· ğ’Ÿ
comp-msub-mren Î¾ Î· (app ğ’Ÿ â„°)    = app & comp-msub-mren Î¾ Î· ğ’Ÿ âŠ— comp-msub-mren Î¾ Î· â„°
comp-msub-mren Î¾ Î· (mvar i)     = sub âˆ™ & comp-get-renâˆ‹ Î¾ Î· i
comp-msub-mren Î¾ Î· (box ğ’Ÿ)      = box & comp-msub-mren Î¾ Î· ğ’Ÿ
comp-msub-mren Î¾ Î· (letbox ğ’Ÿ â„°) = letbox & comp-msub-mren Î¾ Î· ğ’Ÿ
                                         âŠ— ( (\ Î¾â€² â†’ msub Î¾â€² â„°) & comp-mlifts*-gets Î¾ Î· â»Â¹
                                           â‹® comp-msub-mren (mlifts* Î¾) (keep Î·) â„°
                                           )


id-cons-mwk-msub : âˆ€ {Î” Î“ Î A B} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (ğ’Ÿ : Î” âŠ¢ A valid[ âˆ™ ]) (â„° : Î âŠ¢ B valid[ Î“ ])
                                 â†’ (msub (Î¾ , ğ’Ÿ) âˆ˜ mwk) â„° â‰¡ msub Î¾ â„°
id-cons-mwk-msub Î¾ ğ’Ÿ â„° = comp-msub-mren (Î¾ , ğ’Ÿ) (drop id) â„° â»Â¹
                       â‹® (\ Î¾â€² â†’ msub Î¾â€² â„°) & id-gets Î¾


id-cons-mwks-msubs : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (ğ’Ÿ : Î” âŠ¢ A valid[ âˆ™ ]) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ])
                                   â†’ (msubs (Î¾ , ğ’Ÿ) âˆ˜ mwks) Ïˆ â‰¡ msubs Î¾ Ïˆ
id-cons-mwks-msubs Î¾ ğ’Ÿ âˆ™       = refl
id-cons-mwks-msubs Î¾ ğ’Ÿ (Ïˆ , â„°) = _,_ & id-cons-mwks-msubs Î¾ ğ’Ÿ Ïˆ âŠ— id-cons-mwk-msub Î¾ ğ’Ÿ â„°


id-cons-mwks*-msubs* : âˆ€ {Î” Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (ğ’Ÿ : Î” âŠ¢ A valid[ âˆ™ ]) (Ïˆ : Î âŠ¢ Î¨ allvalid*)
                                   â†’ (msubs* (Î¾ , ğ’Ÿ) âˆ˜ mwks*) Ïˆ â‰¡ msubs* Î¾ Ïˆ
id-cons-mwks*-msubs* Î¾ ğ’Ÿ âˆ™       = refl
id-cons-mwks*-msubs* Î¾ ğ’Ÿ (Ïˆ , â„°) = _,_ & id-cons-mwks*-msubs* Î¾ ğ’Ÿ Ïˆ âŠ— id-cons-mwk-msub Î¾ ğ’Ÿ â„°


--------------------------------------------------------------------------------


comp-mren-msub : âˆ€ {Î” Î”â€² Î“ Î A} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid*) (ğ’Ÿ : Î âŠ¢ A valid[ Î“ ])
                                â†’ msub (mrens* Î· Î¾) ğ’Ÿ â‰¡ (mren Î· âˆ˜ msub Î¾) ğ’Ÿ
comp-mren-msub Î· Î¾ (var i)      = refl
comp-mren-msub Î· Î¾ (lam ğ’Ÿ)      = lam & comp-mren-msub Î· Î¾ ğ’Ÿ
comp-mren-msub Î· Î¾ (app ğ’Ÿ â„°)    = app & comp-mren-msub Î· Î¾ ğ’Ÿ âŠ— comp-mren-msub Î· Î¾ â„°
comp-mren-msub Î· Î¾ (mvar i)     = sub âˆ™ & comp-mren-get* Î· Î¾ i
                                â‹® comp-mren-sub Î· âˆ™ (get Î¾ i)
comp-mren-msub Î· Î¾ (box ğ’Ÿ)      = box & comp-mren-msub Î· Î¾ ğ’Ÿ
comp-mren-msub Î· Î¾ (letbox ğ’Ÿ â„°) = letbox & comp-mren-msub Î· Î¾ ğ’Ÿ
                                         âŠ— ( (\ Î¾â€² â†’ msub Î¾â€² â„°) & comp-mlifts*-mrens* Î· Î¾ â»Â¹
                                           â‹® comp-mren-msub (keep Î·) (mlifts* Î¾) â„°
                                           )


comp-mrens-msubs : âˆ€ {Î” Î”â€² Î“ Î Î¨} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ])
                                  â†’ msubs (mrens* Î· Î¾) Ïˆ â‰¡ (mrens Î· âˆ˜ msubs Î¾) Ïˆ
comp-mrens-msubs Î· Î¾ âˆ™       = refl
comp-mrens-msubs Î· Î¾ (Ïˆ , ğ’Ÿ) = _,_ & comp-mrens-msubs Î· Î¾ Ïˆ âŠ— comp-mren-msub Î· Î¾ ğ’Ÿ


comp-mwks-msubs : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ])
                                â†’ (msubs (mlifts* {A} Î¾) âˆ˜ mwks) Ïˆ â‰¡ (mwks âˆ˜ msubs Î¾) Ïˆ
comp-mwks-msubs Î¾ Ïˆ = id-cons-mwks-msubs (mwks* Î¾) mvz Ïˆ
                    â‹® comp-mrens-msubs (drop id) Î¾ Ïˆ


comp-mrens*-msubs* : âˆ€ {Î” Î”â€² Î Î¨} â†’ (Î· : Î”â€² âŠ‡ Î”) (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*)
                                  â†’ msubs* (mrens* Î· Î¾) Ïˆ â‰¡ (mrens* Î· âˆ˜ msubs* Î¾) Ïˆ
comp-mrens*-msubs* Î· Î¾ âˆ™       = refl
comp-mrens*-msubs* Î· Î¾ (Ïˆ , ğ’Ÿ) = _,_ & comp-mrens*-msubs* Î· Î¾ Ïˆ âŠ— comp-mren-msub Î· Î¾ ğ’Ÿ


comp-mwks*-msubs* : âˆ€ {Î” Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*)
                                â†’ (msubs* (mlifts* {A} Î¾) âˆ˜ mwks*) Ïˆ â‰¡ (mwks* âˆ˜ msubs* Î¾) Ïˆ
comp-mwks*-msubs* Î¾ Ïˆ = id-cons-mwks*-msubs* (mwks* Î¾) mvz Ïˆ
                      â‹® comp-mrens*-msubs* (drop id) Î¾ Ïˆ


comp-mlifts*-msubs* : âˆ€ {Î” Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*)
                                  â†’ (msubs* (mlifts* {A} Î¾) âˆ˜ mlifts*) Ïˆ â‰¡ (mlifts* âˆ˜ msubs* Î¾) Ïˆ
comp-mlifts*-msubs* Î¾ Ïˆ = (_, mvz) & comp-mwks*-msubs* Î¾ Ïˆ


--------------------------------------------------------------------------------


comp-msub-sub : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid[ Î“ ]) (ğ’Ÿ : Î âŠ¢ A valid[ Î¨ ])
                              â†’ (sub (msubs Î¾ Ïˆ) âˆ˜ msub Î¾) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ sub Ïˆ) ğ’Ÿ
comp-msub-sub Î¾ Ïˆ (var i)      = comp-msub-get Î¾ Ïˆ i
comp-msub-sub Î¾ Ïˆ (lam ğ’Ÿ)      = lam & ( (\ Î¾â€² â†’ sub Î¾â€² (msub Î¾ ğ’Ÿ)) & comp-msubs-lifts Î¾ Ïˆ
                                       â‹® comp-msub-sub Î¾ (lifts Ïˆ) ğ’Ÿ
                                       )
comp-msub-sub Î¾ Ïˆ (app ğ’Ÿ â„°)    = app & comp-msub-sub Î¾ Ïˆ ğ’Ÿ âŠ— comp-msub-sub Î¾ Ïˆ â„°
comp-msub-sub Î¾ Ïˆ (mvar i)     = comp-sub (msubs Î¾ Ïˆ) âˆ™ (get Î¾ i) â»Â¹
comp-msub-sub Î¾ Ïˆ (box ğ’Ÿ)      = refl
comp-msub-sub Î¾ Ïˆ (letbox ğ’Ÿ â„°) = letbox & comp-msub-sub Î¾ Ïˆ ğ’Ÿ
                                        âŠ— ( (\ Î¾â€² â†’ sub Î¾â€² (msub (mwks* Î¾ , mvz) â„°)) & comp-mwks-msubs Î¾ Ïˆ â»Â¹
                                          â‹® comp-msub-sub (mlifts* Î¾) (mwks Ïˆ) â„°
                                          )


--------------------------------------------------------------------------------


id-msub : âˆ€ {Î” Î“ A} â†’ (ğ’Ÿ : Î” âŠ¢ A valid[ Î“ ])
                    â†’ msub mids* ğ’Ÿ â‰¡ ğ’Ÿ
id-msub (var i)      = refl
id-msub (lam ğ’Ÿ)      = lam & id-msub ğ’Ÿ
id-msub (app ğ’Ÿ â„°)    = app & id-msub ğ’Ÿ âŠ— id-msub â„°
id-msub (mvar i)     = sub âˆ™ & get-mvar i
id-msub (box ğ’Ÿ)      = box & id-msub ğ’Ÿ
id-msub (letbox ğ’Ÿ â„°) = letbox & id-msub ğ’Ÿ âŠ— id-msub â„°


id-msubs : âˆ€ {Î” Î“ Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid[ Î“ ])
                     â†’ msubs mids* Î¾ â‰¡ Î¾
id-msubs âˆ™       = refl
id-msubs (Î¾ , ğ’Ÿ) = _,_ & id-msubs Î¾ âŠ— id-msub ğ’Ÿ


lid-msubs* : âˆ€ {Î” Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid*)
                     â†’ msubs* mids* Î¾ â‰¡ Î¾
lid-msubs* âˆ™       = refl
lid-msubs* (Î¾ , ğ’Ÿ) = _,_ & lid-msubs* Î¾ âŠ— id-msub ğ’Ÿ


rid-msubs* : âˆ€ {Î” Î} â†’ (Î¾ : Î” âŠ¢ Î allvalid*)
                     â†’ msubs* Î¾ mids* â‰¡ Î¾
rid-msubs* âˆ™       = refl
rid-msubs* (Î¾ , ğ’Ÿ) = _,_ & ( id-cons-mwks*-msubs* Î¾ ğ’Ÿ mids*
                           â‹® rid-msubs* Î¾
                           )
                         âŠ— id-sub ğ’Ÿ


comp-msub : âˆ€ {Î” Î“ Î Î¨ A} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*) (ğ’Ÿ : Î¨ âŠ¢ A valid[ Î“ ])
                          â†’ msub (msubs* Î¾ Ïˆ) ğ’Ÿ â‰¡ (msub Î¾ âˆ˜ msub Ïˆ) ğ’Ÿ
comp-msub Î¾ Ïˆ (var i)      = refl
comp-msub Î¾ Ïˆ (lam ğ’Ÿ)      = lam & comp-msub Î¾ Ïˆ ğ’Ÿ
comp-msub Î¾ Ïˆ (app ğ’Ÿ â„°)    = app & comp-msub Î¾ Ïˆ ğ’Ÿ âŠ— comp-msub Î¾ Ïˆ â„°
comp-msub Î¾ Ïˆ (mvar i)     = sub âˆ™ & comp-msub-get* Î¾ Ïˆ i
                           â‹® comp-msub-sub Î¾ âˆ™ (get Ïˆ i)
comp-msub Î¾ Ïˆ (box ğ’Ÿ)      = box & comp-msub Î¾ Ïˆ ğ’Ÿ
comp-msub Î¾ Ïˆ (letbox ğ’Ÿ â„°) = letbox & comp-msub Î¾ Ïˆ ğ’Ÿ
                                    âŠ— ( (\ Î¾â€² â†’ msub Î¾â€² â„°) & comp-mlifts*-msubs* Î¾ Ïˆ â»Â¹
                                      â‹® comp-msub (mlifts* Î¾) (mlifts* Ïˆ) â„°
                                      )


comp-msubs : âˆ€ {Î” Î“ Î Î¨ Î¦} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*) (Ï† : Î¨ âŠ¢ Î¦ allvalid[ Î“ ])
                           â†’ msubs (msubs* Î¾ Ïˆ) Ï† â‰¡ (msubs Î¾ âˆ˜ msubs Ïˆ) Ï†
comp-msubs Î¾ Ïˆ âˆ™       = refl
comp-msubs Î¾ Ïˆ (Ï† , ğ’Ÿ) = _,_ & comp-msubs Î¾ Ïˆ Ï† âŠ— comp-msub Î¾ Ïˆ ğ’Ÿ


assoc-msubs* : âˆ€ {Î” Î Î¨ Î¦} â†’ (Î¾ : Î” âŠ¢ Î allvalid*) (Ïˆ : Î âŠ¢ Î¨ allvalid*) (Ï† : Î¨ âŠ¢ Î¦ allvalid*)
                           â†’ msubs* (msubs* Î¾ Ïˆ) Ï† â‰¡ msubs* Î¾ (msubs* Ïˆ Ï†)
assoc-msubs* Î¾ Ïˆ âˆ™       = refl
assoc-msubs* Î¾ Ïˆ (Ï† , ğ’Ÿ) = _,_ & assoc-msubs* Î¾ Ïˆ Ï† âŠ— comp-msub Î¾ Ïˆ ğ’Ÿ


instance
  ğ’ğŸ’* : Category (List Assert) _âŠ¢_allvalid*
  ğ’ğŸ’* = record
          { id     = mids*
          ; _âˆ˜_    = flip msubs*
          ; lidâˆ˜   = rid-msubs*
          ; ridâˆ˜   = lid-msubs*
          ; assocâˆ˜ = \ Ï† Ïˆ Î¾ â†’ assoc-msubs* Î¾ Ïˆ Ï† â»Â¹
          }


ğ¦ğ¬ğ®ğ› : âˆ€ {A} â†’ Presheaf ğ’ğŸ’* (_âŠ¢ A valid[ âˆ™ ])
ğ¦ğ¬ğ®ğ› = record
         { â„±     = msub
         ; idâ„±   = funext! id-msub
         ; compâ„± = \ Ïˆ Î¾ â†’ funext! (comp-msub Î¾ Ïˆ)
         }


ğ¦ğ¬ğ®ğ›ğ¬ : âˆ€ {Î“ Î} â†’ Presheaf ğ’ğŸ’* (_âŠ¢ Î allvalid[ Î“ ])
ğ¦ğ¬ğ®ğ›ğ¬ = record
          { â„±     = msubs
          ; idâ„±   = funext! id-msubs
          ; compâ„± = \ Ïˆ Î¾ â†’ funext! (comp-msubs Î¾ Ïˆ)
          }


--------------------------------------------------------------------------------
