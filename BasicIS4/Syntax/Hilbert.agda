-- Hilbert-style axiomatic formalisation of syntax.

module BasicIS4.Syntax.Hilbert where

open import BasicIS4.Syntax.Common public


-- Derivations.

infix 3 _âŠ¢_
data _âŠ¢_ (Î“ : Cx Ty) : Ty â†’ Set where
  var   : âˆ€ {A}     â†’ A âˆˆ Î“ â†’ Î“ âŠ¢ A
  app   : âˆ€ {A B}   â†’ Î“ âŠ¢ A â–» B â†’ Î“ âŠ¢ A â†’ Î“ âŠ¢ B
  ci    : âˆ€ {A}     â†’ Î“ âŠ¢ A â–» A
  ck    : âˆ€ {A B}   â†’ Î“ âŠ¢ A â–» B â–» A
  cs    : âˆ€ {A B C} â†’ Î“ âŠ¢ (A â–» B â–» C) â–» (A â–» B) â–» A â–» C
  box   : âˆ€ {A}     â†’ âŒ€ âŠ¢ A â†’ Î“ âŠ¢ â–¡ A
  cdist : âˆ€ {A B}   â†’ Î“ âŠ¢ â–¡ (A â–» B) â–» â–¡ A â–» â–¡ B
  cup   : âˆ€ {A}     â†’ Î“ âŠ¢ â–¡ A â–» â–¡ â–¡ A
  cdown : âˆ€ {A}     â†’ Î“ âŠ¢ â–¡ A â–» A
  cpair : âˆ€ {A B}   â†’ Î“ âŠ¢ A â–» B â–» A âˆ§ B
  cfst  : âˆ€ {A B}   â†’ Î“ âŠ¢ A âˆ§ B â–» A
  csnd  : âˆ€ {A B}   â†’ Î“ âŠ¢ A âˆ§ B â–» B
  tt    : Î“ âŠ¢ âŠ¤

infix 3 _âŠ¢â‹†_
_âŠ¢â‹†_ : Cx Ty â†’ Cx Ty â†’ Set
Î“ âŠ¢â‹† âŒ€     = ğŸ™
Î“ âŠ¢â‹† Î  , A = Î“ âŠ¢â‹† Î  Ã— Î“ âŠ¢ A


-- Monotonicity with respect to context inclusion.

monoâŠ¢ : âˆ€ {A Î“ Î“â€²} â†’ Î“ âŠ† Î“â€² â†’ Î“ âŠ¢ A â†’ Î“â€² âŠ¢ A
monoâŠ¢ Î· (var i)   = var (monoâˆˆ Î· i)
monoâŠ¢ Î· (app t u) = app (monoâŠ¢ Î· t) (monoâŠ¢ Î· u)
monoâŠ¢ Î· ci        = ci
monoâŠ¢ Î· ck        = ck
monoâŠ¢ Î· cs        = cs
monoâŠ¢ Î· (box t)   = box t
monoâŠ¢ Î· cdist     = cdist
monoâŠ¢ Î· cup       = cup
monoâŠ¢ Î· cdown     = cdown
monoâŠ¢ Î· cpair     = cpair
monoâŠ¢ Î· cfst      = cfst
monoâŠ¢ Î· csnd      = csnd
monoâŠ¢ Î· tt        = tt

monoâŠ¢â‹† : âˆ€ {Î  Î“ Î“â€²} â†’ Î“ âŠ† Î“â€² â†’ Î“ âŠ¢â‹† Î  â†’ Î“â€² âŠ¢â‹† Î 
monoâŠ¢â‹† {âŒ€}     Î· âˆ™        = âˆ™
monoâŠ¢â‹† {Î  , A} Î· (ts , t) = monoâŠ¢â‹† Î· ts , monoâŠ¢ Î· t


-- Shorthand for variables.

vâ‚€ : âˆ€ {A Î“} â†’ Î“ , A âŠ¢ A
vâ‚€ = var iâ‚€

vâ‚ : âˆ€ {A B Î“} â†’ (Î“ , A) , B âŠ¢ A
vâ‚ = var iâ‚

vâ‚‚ : âˆ€ {A B C Î“} â†’ ((Î“ , A) , B) , C âŠ¢ A
vâ‚‚ = var iâ‚‚


-- Deduction theorem.

lam : âˆ€ {A B Î“} â†’ Î“ , A âŠ¢ B â†’ Î“ âŠ¢ A â–» B
lam (var top)     = ci
lam (var (pop i)) = app ck (var i)
lam (app t u)     = app (app cs (lam t)) (lam u)
lam ci            = app ck ci
lam ck            = app ck ck
lam cs            = app ck cs
lam (box t)       = app ck (box t)
lam cdist         = app ck cdist
lam cup           = app ck cup
lam cdown         = app ck cdown
lam cpair         = app ck cpair
lam cfst          = app ck cfst
lam csnd          = app ck csnd
lam tt            = app ck tt

lamâ‹† : âˆ€ {Î  Î“ A} â†’ Î“ â§º Î  âŠ¢ A â†’ Î“ âŠ¢ Î  â–»â‹¯â–» A
lamâ‹† {âŒ€}     = id
lamâ‹† {Î  , B} = lamâ‹† {Î } âˆ˜ lam

lamâ‹†â‚€ : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ âŒ€ âŠ¢ Î“ â–»â‹¯â–» A
lamâ‹†â‚€ {âŒ€}     = id
lamâ‹†â‚€ {Î“ , B} = lamâ‹†â‚€ âˆ˜ lam


-- Detachment theorem.

det : âˆ€ {A B Î“} â†’ Î“ âŠ¢ A â–» B â†’ Î“ , A âŠ¢ B
det t = app (monoâŠ¢ weakâŠ† t) vâ‚€

detâ‹† : âˆ€ {Î  Î“ A} â†’ Î“ âŠ¢ Î  â–»â‹¯â–» A â†’ Î“ â§º Î  âŠ¢ A
detâ‹† {âŒ€}     = id
detâ‹† {Î  , B} = det âˆ˜ detâ‹† {Î }

detâ‹†â‚€ : âˆ€ {Î“ A} â†’ âŒ€ âŠ¢ Î“ â–»â‹¯â–» A â†’ Î“ âŠ¢ A
detâ‹†â‚€ {âŒ€}     = id
detâ‹†â‚€ {Î“ , B} = det âˆ˜ detâ‹†â‚€


-- Cut and multicut.

cut : âˆ€ {A B Î“} â†’ Î“ âŠ¢ A â†’ Î“ , A âŠ¢ B â†’ Î“ âŠ¢ B
cut t u = app (lam u) t

multicut : âˆ€ {Î  A Î“} â†’ Î“ âŠ¢â‹† Î  â†’ Î  âŠ¢ A â†’ Î“ âŠ¢ A
multicut {âŒ€}     âˆ™        u = monoâŠ¢ botâŠ† u
multicut {Î  , B} (ts , t) u = app (multicut ts (lam u)) t


-- Reflexivity and transitivity.

reflâŠ¢â‹† : âˆ€ {Î“} â†’ Î“ âŠ¢â‹† Î“
reflâŠ¢â‹† {âŒ€}     = âˆ™
reflâŠ¢â‹† {Î“ , A} = monoâŠ¢â‹† weakâŠ† reflâŠ¢â‹† , vâ‚€

transâŠ¢â‹† : âˆ€ {Î“â€³ Î“â€² Î“} â†’ Î“ âŠ¢â‹† Î“â€² â†’ Î“â€² âŠ¢â‹† Î“â€³ â†’ Î“ âŠ¢â‹† Î“â€³
transâŠ¢â‹† {âŒ€}      ts âˆ™        = âˆ™
transâŠ¢â‹† {Î“â€³ , A} ts (us , u) = transâŠ¢â‹† ts us , multicut ts u


-- Contraction.

ccont : âˆ€ {A B Î“} â†’ Î“ âŠ¢ (A â–» A â–» B) â–» A â–» B
ccont = lam (lam (app (app vâ‚ vâ‚€) vâ‚€))

cont : âˆ€ {A B Î“} â†’ (Î“ , A) , A âŠ¢ B â†’ Î“ , A âŠ¢ B
cont t = det (app ccont (lam (lam t)))


-- Exchange.

cexch : âˆ€ {A B C Î“} â†’ Î“ âŠ¢ (A â–» B â–» C) â–» B â–» A â–» C
cexch = lam (lam (lam (app (app vâ‚‚ vâ‚€) vâ‚)))

exch : âˆ€ {A B C Î“} â†’ (Î“ , A) , B âŠ¢ C â†’ (Î“ , B) , A âŠ¢ C
exch t = det (det (app cexch (lam (lam t))))


-- Composition.

ccomp : âˆ€ {A B C Î“} â†’ Î“ âŠ¢ (B â–» C) â–» (A â–» B) â–» A â–» C
ccomp = lam (lam (lam (app vâ‚‚ (app vâ‚ vâ‚€))))

comp : âˆ€ {A B C Î“} â†’ Î“ , B âŠ¢ C â†’ Î“ , A âŠ¢ B â†’ Î“ , A âŠ¢ C
comp t u = det (app (app ccomp (lam t)) (lam u))


-- Useful theorems in functional form.

dist : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ (A â–» B) â†’ Î“ âŠ¢ â–¡ A â†’ Î“ âŠ¢ â–¡ B
dist t u = app (app cdist t) u

up : âˆ€ {A Î“} â†’ Î“ âŠ¢ â–¡ A â†’ Î“ âŠ¢ â–¡ â–¡ A
up t = app cup t

down : âˆ€ {A Î“} â†’ Î“ âŠ¢ â–¡ A â†’ Î“ âŠ¢ A
down t = app cdown t

distup : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ (â–¡ A â–» B) â†’ Î“ âŠ¢ â–¡ A â†’ Î“ âŠ¢ â–¡ B
distup t u = dist t (up u)

unbox : âˆ€ {A C Î“} â†’ Î“ âŠ¢ â–¡ A â†’ Î“ , â–¡ A âŠ¢ C â†’ Î“ âŠ¢ C
unbox t u = app (lam u) t

pair : âˆ€ {A B Î“} â†’ Î“ âŠ¢ A â†’ Î“ âŠ¢ B â†’ Î“ âŠ¢ A âˆ§ B
pair t u = app (app cpair t) u

fst : âˆ€ {A B Î“} â†’ Î“ âŠ¢ A âˆ§ B â†’ Î“ âŠ¢ A
fst t = app cfst t

snd : âˆ€ {A B Î“} â†’ Î“ âŠ¢ A âˆ§ B â†’ Î“ âŠ¢ B
snd t = app csnd t


-- Internalisation, or lifting, and additional theorems.

lift : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ â–¡â‹† Î“ âŠ¢ â–¡ A
lift {âŒ€}     t = box t
lift {Î“ , B} t = det (app cdist (lift (lam t)))

hypup : âˆ€ {A B Î“} â†’ Î“ âŠ¢ A â–» B â†’ Î“ âŠ¢ â–¡ A â–» B
hypup t = lam (app (monoâŠ¢ weakâŠ† t) (down vâ‚€))

hypdown : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ â–¡ A â–» B â†’ Î“ âŠ¢ â–¡ A â–» B
hypdown t = lam (app (monoâŠ¢ weakâŠ† t) (up vâ‚€))

cxup : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ â–¡â‹† Î“ âŠ¢ A
cxup {âŒ€}     t = t
cxup {Î“ , B} t = det (hypup (cxup (lam t)))

cxdown : âˆ€ {Î“ A} â†’ â–¡â‹† â–¡â‹† Î“ âŠ¢ A â†’ â–¡â‹† Î“ âŠ¢ A
cxdown {âŒ€}     t = t
cxdown {Î“ , B} t = det (hypdown (cxdown (lam t)))

boxâ‹† : âˆ€ {Î  Î“} â†’ âŒ€ âŠ¢â‹† Î  â†’ Î“ âŠ¢â‹† â–¡â‹† Î 
boxâ‹† {âŒ€}     âˆ™        = âˆ™
boxâ‹† {Î  , A} (ts , t) = boxâ‹† ts , box t

liftâ‹† : âˆ€ {Î  Î“} â†’ Î“ âŠ¢â‹† Î  â†’ â–¡â‹† Î“ âŠ¢â‹† â–¡â‹† Î 
liftâ‹† {âŒ€}     âˆ™        = âˆ™
liftâ‹† {Î  , A} (ts , t) = liftâ‹† ts , lift t

upâ‹† : âˆ€ {Î  Î“} â†’ Î“ âŠ¢â‹† â–¡â‹† Î  â†’ Î“ âŠ¢â‹† â–¡â‹† â–¡â‹† Î 
upâ‹† {âŒ€}     âˆ™        = âˆ™
upâ‹† {Î  , A} (ts , t) = upâ‹† ts , up t

downâ‹† : âˆ€ {Î  Î“} â†’ Î“ âŠ¢â‹† â–¡â‹† Î  â†’ Î“ âŠ¢â‹† Î 
downâ‹† {âŒ€}     âˆ™        = âˆ™
downâ‹† {Î  , A} (ts , t) = downâ‹† ts , down t

multibox : âˆ€ {Î  A Î“} â†’ Î“ âŠ¢â‹† â–¡â‹† Î  â†’ â–¡â‹† Î  âŠ¢ A â†’ Î“ âŠ¢ â–¡ A
multibox ts u = multicut (upâ‹† ts) (lift u)

distâ€² : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ (A â–» B) â†’ Î“ âŠ¢ â–¡ A â–» â–¡ B
distâ€² t = lam (dist (monoâŠ¢ weakâŠ† t) vâ‚€)

mpair : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ A â†’ Î“ âŠ¢ â–¡ B â†’ Î“ âŠ¢ â–¡ (A âˆ§ B)
mpair t u = dist (dist (box cpair) t) u

mfst : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ (A âˆ§ B) â†’ Î“ âŠ¢ â–¡ A
mfst t = dist (box cfst) t

msnd : âˆ€ {A B Î“} â†’ Î“ âŠ¢ â–¡ (A âˆ§ B) â†’ Î“ âŠ¢ â–¡ B
msnd t = dist (box csnd) t


-- Closure under context concatenation.

concat : âˆ€ {A B Î“} Î“â€² â†’ Î“ , A âŠ¢ B â†’ Î“â€² âŠ¢ A â†’ Î“ â§º Î“â€² âŠ¢ B
concat Î“â€² t u = app (monoâŠ¢ (weakâŠ†â§ºâ‚— Î“â€²) (lam t)) (monoâŠ¢ weakâŠ†â§ºáµ£ u)


-- Conversion.

data _â‹™_ {Î“ : Cx Ty} : âˆ€ {A} â†’ Î“ âŠ¢ A â†’ Î“ âŠ¢ A â†’ Set where
  reflâ‹™     : âˆ€ {A} {t : Î“ âŠ¢ A}
               â†’ t â‹™ t
  transâ‹™    : âˆ€ {A} {t tâ€² tâ€³ : Î“ âŠ¢ A}
               â†’ t â‹™ tâ€² â†’ tâ€² â‹™ tâ€³ â†’ t â‹™ tâ€³
  symâ‹™      : âˆ€ {A} {t tâ€² : Î“ âŠ¢ A}
               â†’ t â‹™ tâ€² â†’ tâ€² â‹™ t
  congappâ‹™  : âˆ€ {A B} {t tâ€² : Î“ âŠ¢ A â–» B} {u uâ€² : Î“ âŠ¢ A}
               â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
               â†’ app t u â‹™ app tâ€² uâ€²
  congiâ‹™    : âˆ€ {A} {t tâ€² : Î“ âŠ¢ A}
               â†’ t â‹™ tâ€²
               â†’ app ci t â‹™ app ci tâ€²
  congkâ‹™    : âˆ€ {A B} {t tâ€² : Î“ âŠ¢ A} {u uâ€² : Î“ âŠ¢ B}
               â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
               â†’ app (app ck t) u â‹™ app (app ck tâ€²) uâ€²
  congsâ‹™    : âˆ€ {A B C} {t tâ€² : Î“ âŠ¢ A â–» B â–» C} {u uâ€² : Î“ âŠ¢ A â–» B} {v vâ€² : Î“ âŠ¢ A}
               â†’ t â‹™ tâ€² â†’ u â‹™ uâ€² â†’ v â‹™ vâ€²
               â†’ app (app (app cs t) u) v â‹™ app (app (app cs tâ€²) uâ€²) vâ€²
  -- NOTE: Rejected by Pfenning and Davies.
  -- congboxâ‹™  : âˆ€ {A} {t tâ€² : âŒ€ âŠ¢ A}
  --              â†’ t â‹™ tâ€²
  --              â†’ box {Î“} t â‹™ box {Î“} tâ€²
  congdistâ‹™ : âˆ€ {A B} {t tâ€² : Î“ âŠ¢ â–¡ (A â–» B)} {u uâ€² : Î“ âŠ¢ â–¡ A}
               â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
               â†’ app (app cdist t) u â‹™ app (app cdist tâ€²) uâ€²
  congupâ‹™   : âˆ€ {A} {t tâ€² : Î“ âŠ¢ â–¡ A}
               â†’ t â‹™ tâ€²
               â†’ app cup t â‹™ app cup tâ€²
  congdownâ‹™ : âˆ€ {A} {t tâ€² : Î“ âŠ¢ â–¡ A}
               â†’ t â‹™ tâ€²
               â†’ app cdown t â‹™ app cdown tâ€²
  congpairâ‹™ : âˆ€ {A B} {t tâ€² : Î“ âŠ¢ A} {u uâ€² : Î“ âŠ¢ B}
               â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
               â†’ app (app cpair t) u â‹™ app (app cpair tâ€²) uâ€²
  congfstâ‹™  : âˆ€ {A B} {t tâ€² : Î“ âŠ¢ A âˆ§ B}
               â†’ t â‹™ tâ€²
               â†’ app cfst t â‹™ app cfst tâ€²
  congsndâ‹™  : âˆ€ {A B} {t tâ€² : Î“ âŠ¢ A âˆ§ B}
               â†’ t â‹™ tâ€²
               â†’ app csnd t â‹™ app csnd tâ€²
  -- TODO: Verify this.
  betaâ–»â‚–â‹™   : âˆ€ {A B} {t : Î“ âŠ¢ A} {u : Î“ âŠ¢ B}
               â†’ app (app ck t) u â‹™ t
  -- TODO: Verify this.
  betaâ–»â‚›â‹™   : âˆ€ {A B C} {t : Î“ âŠ¢ A â–» B â–» C} {u : Î“ âŠ¢ A â–» B} {v : Î“ âŠ¢ A}
               â†’ app (app (app cs t) u) v â‹™ app (app t v) (app u v)
  -- TODO: What about eta for â–»; beta and eta for â–¡?
  betaâˆ§â‚â‹™   : âˆ€ {A B} {t : Î“ âŠ¢ A} {u : Î“ âŠ¢ B}
               â†’ app cfst (app (app cpair t) u) â‹™ t
  betaâˆ§â‚‚â‹™   : âˆ€ {A B} {t : Î“ âŠ¢ A} {u : Î“ âŠ¢ B}
               â†’ app csnd (app (app cpair t) u) â‹™ u
  etaâˆ§â‹™     : âˆ€ {A B} {t : Î“ âŠ¢ A âˆ§ B}
               â†’ t â‹™ app (app cpair (app cfst t)) (app csnd t)
  etaâŠ¤â‹™    : âˆ€ {t : Î“ âŠ¢ âŠ¤}
               â†’ t â‹™ tt
