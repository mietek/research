-- Gentzen-style formalisation of syntax with context pairs, after Pfenning-Davies.

module BasicIS4.Syntax.DyadicGentzen where

open import BasicIS4.Syntax.Common public


-- Derivations.

infix 3 _âŠ¢_
data _âŠ¢_ : CxÂ² Ty Ty â†’ Ty â†’ Set where
  var   : âˆ€ {A Î“ Î”}   â†’ A âˆˆ Î“ â†’ Î“ â Î” âŠ¢ A
  lam   : âˆ€ {A B Î“ Î”} â†’ Î“ , A â Î” âŠ¢ B â†’ Î“ â Î” âŠ¢ A â–» B
  app   : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â–» B â†’ Î“ â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ B
  mvar  : âˆ€ {A Î“ Î”}   â†’ A âˆˆ Î” â†’ Î“ â Î” âŠ¢ A
  box   : âˆ€ {A Î“ Î”}   â†’ âˆ… â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ â–¡ A
  unbox : âˆ€ {A C Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” , A âŠ¢ C â†’ Î“ â Î” âŠ¢ C
  pair  : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ B â†’ Î“ â Î” âŠ¢ A âˆ§ B
  fst   : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A âˆ§ B â†’ Î“ â Î” âŠ¢ A
  snd   : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A âˆ§ B â†’ Î“ â Î” âŠ¢ B
  tt    : âˆ€ {Î“ Î”}     â†’ Î“ â Î” âŠ¢ âŠ¤

infix 3 _âŠ¢â‹†_
_âŠ¢â‹†_ : CxÂ² Ty Ty â†’ Cx Ty â†’ Set
Î“ â Î” âŠ¢â‹† âˆ…     = ğŸ™
Î“ â Î” âŠ¢â‹† Î , A = Î“ â Î” âŠ¢â‹† Î Ã— Î“ â Î” âŠ¢ A


-- Monotonicity with respect to context inclusion.

monoâŠ¢ : âˆ€ {A Î“ Î“â€² Î”} â†’ Î“ âŠ† Î“â€² â†’ Î“ â Î” âŠ¢ A â†’ Î“â€² â Î” âŠ¢ A
monoâŠ¢ Î· (var i)     = var (monoâˆˆ Î· i)
monoâŠ¢ Î· (lam t)     = lam (monoâŠ¢ (keep Î·) t)
monoâŠ¢ Î· (app t u)   = app (monoâŠ¢ Î· t) (monoâŠ¢ Î· u)
monoâŠ¢ Î· (mvar i)    = mvar i
monoâŠ¢ Î· (box t)     = box t
monoâŠ¢ Î· (unbox t u) = unbox (monoâŠ¢ Î· t) (monoâŠ¢ Î· u)
monoâŠ¢ Î· (pair t u)  = pair (monoâŠ¢ Î· t) (monoâŠ¢ Î· u)
monoâŠ¢ Î· (fst t)     = fst (monoâŠ¢ Î· t)
monoâŠ¢ Î· (snd t)     = snd (monoâŠ¢ Î· t)
monoâŠ¢ Î· tt          = tt

monoâŠ¢â‹† : âˆ€ {Î Î“ Î“â€² Î”} â†’ Î“ âŠ† Î“â€² â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î“â€² â Î” âŠ¢â‹† Î
monoâŠ¢â‹† {âˆ…}     Î· âˆ™        = âˆ™
monoâŠ¢â‹† {Î , A} Î· (ts , t) = monoâŠ¢â‹† Î· ts , monoâŠ¢ Î· t


-- Monotonicity with respect to modal context inclusion.

mmonoâŠ¢ : âˆ€ {A Î“ Î” Î”â€²} â†’ Î” âŠ† Î”â€² â†’ Î“ â Î” âŠ¢ A â†’ Î“ â Î”â€² âŠ¢ A
mmonoâŠ¢ Î¸ (var i)     = var i
mmonoâŠ¢ Î¸ (lam t)     = lam (mmonoâŠ¢ Î¸ t)
mmonoâŠ¢ Î¸ (app t u)   = app (mmonoâŠ¢ Î¸ t) (mmonoâŠ¢ Î¸ u)
mmonoâŠ¢ Î¸ (mvar i)    = mvar (monoâˆˆ Î¸ i)
mmonoâŠ¢ Î¸ (box t)     = box (mmonoâŠ¢ Î¸ t)
mmonoâŠ¢ Î¸ (unbox t u) = unbox (mmonoâŠ¢ Î¸ t) (mmonoâŠ¢ (keep Î¸) u)
mmonoâŠ¢ Î¸ (pair t u)  = pair (mmonoâŠ¢ Î¸ t) (mmonoâŠ¢ Î¸ u)
mmonoâŠ¢ Î¸ (fst t)     = fst (mmonoâŠ¢ Î¸ t)
mmonoâŠ¢ Î¸ (snd t)     = snd (mmonoâŠ¢ Î¸ t)
mmonoâŠ¢ Î¸ tt          = tt

mmonoâŠ¢â‹† : âˆ€ {Î Î“ Î” Î”â€²} â†’ Î” âŠ† Î”â€² â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î“ â Î”â€² âŠ¢â‹† Î
mmonoâŠ¢â‹† {âˆ…}     Î¸ âˆ™        = âˆ™
mmonoâŠ¢â‹† {Î , A} Î¸ (ts , t) = mmonoâŠ¢â‹† Î¸ ts , mmonoâŠ¢ Î¸ t


-- Monotonicity using context pairs.

monoÂ²âŠ¢ : âˆ€ {A Î  Î â€²} â†’ Î  âŠ†Â² Î â€² â†’ Î  âŠ¢ A â†’ Î â€² âŠ¢ A
monoÂ²âŠ¢ (Î· , Î¸) = monoâŠ¢ Î· âˆ˜ mmonoâŠ¢ Î¸


-- Shorthand for variables.

vâ‚€ : âˆ€ {A Î“ Î”} â†’ Î“ , A â Î” âŠ¢ A
vâ‚€ = var iâ‚€

vâ‚ : âˆ€ {A B Î“ Î”} â†’ Î“ , A , B â Î” âŠ¢ A
vâ‚ = var iâ‚

vâ‚‚ : âˆ€ {A B C Î“ Î”} â†’ Î“ , A , B , C â Î” âŠ¢ A
vâ‚‚ = var iâ‚‚

mvâ‚€ : âˆ€ {A Î“ Î”} â†’ Î“ â Î” , A âŠ¢ A
mvâ‚€ = mvar iâ‚€

mvâ‚ : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” , A , B âŠ¢ A
mvâ‚ = mvar iâ‚

mvâ‚‚ : âˆ€ {A B C Î“ Î”} â†’ Î“ â Î” , A , B , C âŠ¢ A
mvâ‚‚ = mvar iâ‚‚


-- Reflexivity.

reflâŠ¢â‹†â‚€ : âˆ€ {Î“} â†’ Î“ â âˆ… âŠ¢â‹† Î“
reflâŠ¢â‹†â‚€ {âˆ…}     = âˆ™
reflâŠ¢â‹†â‚€ {Î“ , A} = monoâŠ¢â‹† weakâŠ† reflâŠ¢â‹†â‚€ , vâ‚€

reflâŠ¢â‹† : âˆ€ {Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† Î“
reflâŠ¢â‹† = mmonoâŠ¢â‹† botâŠ† reflâŠ¢â‹†â‚€

mreflâŠ¢â‹†â‚€ : âˆ€ {Î”} â†’ âˆ… â Î” âŠ¢â‹† â–¡â‹† Î”
mreflâŠ¢â‹†â‚€ {âˆ…}     = âˆ™
mreflâŠ¢â‹†â‚€ {Î” , A} = mmonoâŠ¢â‹† weakâŠ† mreflâŠ¢â‹†â‚€ , box mvâ‚€

mreflâŠ¢â‹† : âˆ€ {Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Î”
mreflâŠ¢â‹† = monoâŠ¢â‹† botâŠ† mreflâŠ¢â‹†â‚€

mreflâŠ¢â‹†â€² : âˆ€ {Î” Î”â€² Î“ Î“â€²} â†’ (âˆ€ {A} â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“â€² â Î”â€² âŠ¢ A) â†’ Î“â€² â Î”â€² âŠ¢â‹† Î”
mreflâŠ¢â‹†â€² {âˆ…}     f = âˆ™
mreflâŠ¢â‹†â€² {Î” , B} f = mreflâŠ¢â‹†â€² (f âˆ˜ mmonoâŠ¢ weakâŠ†) , f (box mvâ‚€)


-- Deduction theorem is built-in.

lamâ‹† : âˆ€ {Î A Î“ Î”} â†’ Î“ â§º Î â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ Î â–»â‹¯â–» A
lamâ‹† {âˆ…}     = I
lamâ‹† {Î , B} = lamâ‹† {Î} âˆ˜ lam

lamâ‹†â‚€ : âˆ€ {Î“ A Î”} â†’ Î“ â Î” âŠ¢ A â†’ âˆ… â Î” âŠ¢ Î“ â–»â‹¯â–» A
lamâ‹†â‚€ {âˆ…}     = I
lamâ‹†â‚€ {Î“ , B} = lamâ‹†â‚€ âˆ˜ lam


-- Modal deduction theorem.

mlam : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” , A âŠ¢ B â†’ Î“ â Î” âŠ¢ â–¡ A â–» B
mlam t = lam (unbox vâ‚€ (monoâŠ¢ weakâŠ† t))

mlamâ‹† : âˆ€ {Î A Î“ Î”} â†’ Î“ â Î” â§º Î âŠ¢ A â†’ Î“ â Î” âŠ¢ â–¡â‹† Î â–»â‹¯â–» A
mlamâ‹† {âˆ…}     = I
mlamâ‹† {Î , B} = mlamâ‹† {Î} âˆ˜ mlam

mlamâ‹†â‚€ : âˆ€ {Î” A Î“} â†’ Î“ â Î” âŠ¢ A â†’ Î“ â âˆ… âŠ¢ â–¡â‹† Î” â–»â‹¯â–» A
mlamâ‹†â‚€ {âˆ…}     = I
mlamâ‹†â‚€ {Î” , B} = mlamâ‹†â‚€ âˆ˜ mlam


-- Detachment theorems.

det : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â–» B â†’ Î“ , A â Î” âŠ¢ B
det t = app (monoâŠ¢ weakâŠ† t) vâ‚€

detâ‹† : âˆ€ {Î A Î“ Î”} â†’ Î“ â Î” âŠ¢ Î â–»â‹¯â–» A â†’ Î“ â§º Î â Î” âŠ¢ A
detâ‹† {âˆ…}     = I
detâ‹† {Î , B} = det âˆ˜ detâ‹† {Î}

detâ‹†â‚€ : âˆ€ {Î“ A Î”} â†’ âˆ… â Î” âŠ¢ Î“ â–»â‹¯â–» A â†’ Î“ â Î” âŠ¢ A
detâ‹†â‚€ {âˆ…}     = I
detâ‹†â‚€ {Î“ , B} = det âˆ˜ detâ‹†â‚€

mdet : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â–» B â†’ Î“ â Î” , A âŠ¢ B
mdet t = app (mmonoâŠ¢ weakâŠ† t) (box mvâ‚€)

mdetâ‹† : âˆ€ {Î A Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡â‹† Î â–»â‹¯â–» A â†’ Î“ â Î” â§º Î âŠ¢ A
mdetâ‹† {âˆ…}     = I
mdetâ‹† {Î , B} = mdet âˆ˜ mdetâ‹† {Î}

mdetâ‹†â‚€ : âˆ€ {Î” A Î“} â†’ Î“ â âˆ… âŠ¢ â–¡â‹† Î” â–»â‹¯â–» A â†’ Î“ â Î” âŠ¢ A
mdetâ‹†â‚€ {âˆ…}     = I
mdetâ‹†â‚€ {Î” , B} = mdet âˆ˜ mdetâ‹†â‚€


-- Context manipulation.

merge : âˆ€ {Î” A Î“} â†’ Î“ â Î” âŠ¢ A â†’ Î“ â§º (â–¡â‹† Î”) â âˆ… âŠ¢ A
merge {Î”} = detâ‹† {â–¡â‹† Î”} âˆ˜ mlamâ‹†â‚€

mmerge : âˆ€ {Î“ A Î”} â†’ â–¡â‹† Î“ â Î” âŠ¢ A â†’ âˆ… â Î” â§º Î“ âŠ¢ A
mmerge {Î“} = mdetâ‹† {Î“} âˆ˜ lamâ‹†â‚€

split : âˆ€ {Î” A Î“} â†’ Î“ â§º (â–¡â‹† Î”) â âˆ… âŠ¢ A â†’ Î“ â Î” âŠ¢ A
split {Î”} = mdetâ‹†â‚€ âˆ˜ lamâ‹† {â–¡â‹† Î”}

msplit : âˆ€ {Î“ A Î”} â†’ âˆ… â Î” â§º Î“ âŠ¢ A â†’ â–¡â‹† Î“ â Î” âŠ¢ A
msplit {Î“} = detâ‹†â‚€ âˆ˜ mlamâ‹† {Î“}

mergeâ‹† : âˆ€ {Î Î” Î“} â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î“ â§º (â–¡â‹† Î”) â âˆ… âŠ¢â‹† Î
mergeâ‹† {âˆ…}     âˆ™        = âˆ™
mergeâ‹† {Î , A} (ts , t) = mergeâ‹† ts , merge t

splitâ‹† : âˆ€ {Î Î” Î“} â†’ Î“ â§º (â–¡â‹† Î”) â âˆ… âŠ¢â‹† Î â†’ Î“ â Î” âŠ¢â‹† Î
splitâ‹† {âˆ…}     âˆ™        = âˆ™
splitâ‹† {Î , A} (ts , t) = splitâ‹† ts , split t


-- Cut and multicut.

cut : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â†’ Î“ , A â Î” âŠ¢ B â†’ Î“ â Î” âŠ¢ B
cut t u = app (lam u) t

mcut : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” , A âŠ¢ B â†’ Î“ â Î” âŠ¢ B
mcut t u = app (mlam u) t

multicut : âˆ€ {Î A Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ A
multicut {âˆ…}     âˆ™        u = monoâŠ¢ botâŠ† u
multicut {Î , B} (ts , t) u = app (multicut ts (lam u)) t

mmulticut : âˆ€ {Î A Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Î â†’ Î“ â Î âŠ¢ A â†’ Î“ â Î” âŠ¢ A
mmulticut {âˆ…}     âˆ™        u = mmonoâŠ¢ botâŠ† u
mmulticut {Î , B} (ts , t) u = app (mmulticut ts (mlam u)) t

multicutÂ² : âˆ€ {Î Îâ€² A Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Îâ€² â†’ Î â Îâ€² âŠ¢ A â†’ Î“ â Î” âŠ¢ A
multicutÂ² {âˆ…}     âˆ™        us v = mmulticut us (monoâŠ¢ botâŠ† v)
multicutÂ² {Î , B} (ts , t) us v = app (multicutÂ² ts us (lam v)) t


-- Tansitivity.

transâŠ¢â‹†â‚€ : âˆ€ {Î“â€³ Î“â€² Î“} â†’ Î“ â âˆ… âŠ¢â‹† Î“â€² â†’ Î“â€² â âˆ… âŠ¢â‹† Î“â€³ â†’ Î“ â âˆ… âŠ¢â‹† Î“â€³
transâŠ¢â‹†â‚€ {âˆ…}      ts âˆ™        = âˆ™
transâŠ¢â‹†â‚€ {Î“â€³ , A} ts (us , u) = transâŠ¢â‹†â‚€ ts us , multicut ts u

transâŠ¢â‹† : âˆ€ {Î Î“ Î“â€² Î” Î”â€²} â†’ Î“ â Î” âŠ¢â‹† Î“â€² â§º (â–¡â‹† Î”â€²) â†’ Î“â€² â Î”â€² âŠ¢â‹† Î â†’ Î“ â Î” âŠ¢â‹† Î
transâŠ¢â‹† ts us = splitâ‹† (transâŠ¢â‹†â‚€ (mergeâ‹† ts) (mergeâ‹† us))


-- Contraction.

ccont : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ (A â–» A â–» B) â–» A â–» B
ccont = lam (lam (app (app vâ‚ vâ‚€) vâ‚€))

cont : âˆ€ {A B Î“ Î”} â†’ Î“ , A , A â Î” âŠ¢ B â†’ Î“ , A â Î” âŠ¢ B
cont t = det (app ccont (lam (lam t)))

mcont : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” , A , A âŠ¢ B â†’ Î“ â Î” , A âŠ¢ B
mcont t = mdet (app ccont (mlam (mlam t)))


-- Exchange, or SchÃ¶nfinkelâ€™s C combinator.

cexch : âˆ€ {A B C Î“ Î”} â†’ Î“ â Î” âŠ¢ (A â–» B â–» C) â–» B â–» A â–» C
cexch = lam (lam (lam (app (app vâ‚‚ vâ‚€) vâ‚)))

exch : âˆ€ {A B C Î“ Î”} â†’ Î“ , A , B â Î” âŠ¢ C â†’ Î“ , B , A â Î” âŠ¢ C
exch t = det (det (app cexch (lam (lam t))))

mexch : âˆ€ {A B C Î“ Î”} â†’ Î“ â Î” , A , B âŠ¢ C â†’ Î“ â Î” , B , A âŠ¢ C
mexch t = mdet (mdet (app cexch (mlam (mlam t))))


-- Composition, or SchÃ¶nfinkelâ€™s B combinator.

ccomp : âˆ€ {A B C Î“ Î”} â†’ Î“ â Î” âŠ¢ (B â–» C) â–» (A â–» B) â–» A â–» C
ccomp = lam (lam (lam (app vâ‚‚ (app vâ‚ vâ‚€))))

comp : âˆ€ {A B C Î“ Î”} â†’ Î“ , B â Î” âŠ¢ C â†’ Î“ , A â Î” âŠ¢ B â†’ Î“ , A â Î” âŠ¢ C
comp t u = det (app (app ccomp (lam t)) (lam u))

mcomp : âˆ€ {A B C Î“ Î”} â†’ Î“ â Î” , B âŠ¢ â–¡ C â†’ Î“ â Î” , A âŠ¢ â–¡ B â†’ Î“ â Î” , A âŠ¢ â–¡ C
mcomp t u = mdet (app (app ccomp (mlam t)) (mlam u))


-- Useful theorems in functional form.

dist : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (A â–» B) â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” âŠ¢ â–¡ B
dist t u = unbox t (unbox (mmonoâŠ¢ weakâŠ† u) (box (app mvâ‚ mvâ‚€)))

up : âˆ€ {A Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” âŠ¢ â–¡ â–¡ A
up t = unbox t (box (box mvâ‚€))

down : âˆ€ {A Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” âŠ¢ A
down t = unbox t mvâ‚€

distup : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (â–¡ A â–» B) â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” âŠ¢ â–¡ B
distup t u = dist t (up u)


-- Useful theorems in combinatory form.

ci : âˆ€ {A Î“ Î”} â†’ Î“ â Î” âŠ¢ A â–» A
ci = lam vâ‚€

ck : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â–» B â–» A
ck = lam (lam vâ‚)

cs : âˆ€ {A B C Î“ Î”} â†’ Î“ â Î” âŠ¢ (A â–» B â–» C) â–» (A â–» B) â–» A â–» C
cs = lam (lam (lam (app (app vâ‚‚ vâ‚€) (app vâ‚ vâ‚€))))

cdist : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (A â–» B) â–» â–¡ A â–» â–¡ B
cdist = lam (lam (dist vâ‚ vâ‚€))

cup : âˆ€ {A Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â–» â–¡ â–¡ A
cup = lam (up vâ‚€)

cdown : âˆ€ {A Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â–» A
cdown = lam (down vâ‚€)

cdistup : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (â–¡ A â–» B) â–» â–¡ A â–» â–¡ B
cdistup = lam (lam (dist vâ‚ (up vâ‚€)))

cunbox : âˆ€ {A C Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â–» (â–¡ A â–» C) â–» C
cunbox = lam (lam (app vâ‚€ vâ‚))

cpair : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â–» B â–» A âˆ§ B
cpair = lam (lam (pair vâ‚ vâ‚€))

cfst : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A âˆ§ B â–» A
cfst = lam (fst vâ‚€)

csnd : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A âˆ§ B â–» B
csnd = lam (snd vâ‚€)


-- Internalisation, or lifting, and additional theorems.

lift : âˆ€ {Î“ A Î”} â†’ Î“ â Î” âŠ¢ A â†’ â–¡â‹† Î“ â Î” âŠ¢ â–¡ A
lift {âˆ…}     t = box t
lift {Î“ , B} t = det (app cdist (lift (lam t)))

hypup : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ A â–» B â†’ Î“ â Î” âŠ¢ â–¡ A â–» B
hypup t = lam (app (monoâŠ¢ weakâŠ† t) (down vâ‚€))

hypdown : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ â–¡ A â–» B â†’ Î“ â Î” âŠ¢ â–¡ A â–» B
hypdown t = lam (app (monoâŠ¢ weakâŠ† t) (up vâ‚€))

cxup : âˆ€ {Î“ A Î”} â†’ Î“ â Î” âŠ¢ A â†’ â–¡â‹† Î“ â Î” âŠ¢ A
cxup {âˆ…}     t = t
cxup {Î“ , B} t = det (hypup (cxup (lam t)))

cxdown : âˆ€ {Î“ A Î”} â†’ â–¡â‹† â–¡â‹† Î“ â Î” âŠ¢ A â†’ â–¡â‹† Î“ â Î” âŠ¢ A
cxdown {âˆ…}     t = t
cxdown {Î“ , B} t = det (hypdown (cxdown (lam t)))

boxâ‹† : âˆ€ {Î Î“ Î”} â†’ âˆ… â Î” âŠ¢â‹† Î â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Î
boxâ‹† {âˆ…}     âˆ™        = âˆ™
boxâ‹† {Î , A} (ts , t) = boxâ‹† ts , box t

liftâ‹† : âˆ€ {Î Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† Î â†’ â–¡â‹† Î“ â Î” âŠ¢â‹† â–¡â‹† Î
liftâ‹† {âˆ…}     âˆ™        = âˆ™
liftâ‹† {Î , A} (ts , t) = liftâ‹† ts , lift t

upâ‹† : âˆ€ {Î Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Î â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† â–¡â‹† Î
upâ‹† {âˆ…}     âˆ™        = âˆ™
upâ‹† {Î , A} (ts , t) = upâ‹† ts , up t

downâ‹† : âˆ€ {Î Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Î â†’ Î“ â Î” âŠ¢â‹† Î
downâ‹† {âˆ…}     âˆ™        = âˆ™
downâ‹† {Î , A} (ts , t) = downâ‹† ts , down t

multibox : âˆ€ {Î A Î“ Î”} â†’ Î“ â Î” âŠ¢â‹† â–¡â‹† Î â†’ â–¡â‹† Î â âˆ… âŠ¢ A â†’ Î“ â Î” âŠ¢ â–¡ A
multibox ts u = multicut (upâ‹† ts) (mmonoâŠ¢ botâŠ† (lift u))

distâ€² : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (A â–» B) â†’ Î“ â Î” âŠ¢ â–¡ A â–» â–¡ B
distâ€² t = lam (dist (monoâŠ¢ weakâŠ† t) vâ‚€)

mpair : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ A â†’ Î“ â Î” âŠ¢ â–¡ B â†’ Î“ â Î” âŠ¢ â–¡ (A âˆ§ B)
mpair t u = unbox t (unbox (mmonoâŠ¢ weakâŠ† u) (box (pair mvâ‚ mvâ‚€)))

mfst : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (A âˆ§ B) â†’ Î“ â Î” âŠ¢ â–¡ A
mfst t = unbox t (box (fst mvâ‚€))

msnd : âˆ€ {A B Î“ Î”} â†’ Î“ â Î” âŠ¢ â–¡ (A âˆ§ B) â†’ Î“ â Î” âŠ¢ â–¡ B
msnd t = unbox t (box (snd mvâ‚€))


-- Closure under context concatenation.

concat : âˆ€ {A B Î“} Î“â€² {Î”} â†’ Î“ , A â Î” âŠ¢ B â†’ Î“â€² â Î” âŠ¢ A â†’ Î“ â§º Î“â€² â Î” âŠ¢ B
concat Î“â€² t u = app (monoâŠ¢ (weakâŠ†â§ºâ‚ Î“â€²) (lam t)) (monoâŠ¢ weakâŠ†â§ºâ‚‚ u)

mconcat : âˆ€ {A B Î“ Î”} Î”â€² â†’ Î“ â Î” , A âŠ¢ B â†’ Î“ â Î”â€² âŠ¢ â–¡ A â†’ Î“ â Î” â§º Î”â€² âŠ¢ B
mconcat Î”â€² t u = app (mmonoâŠ¢ (weakâŠ†â§ºâ‚ Î”â€²) (mlam t)) (mmonoâŠ¢ weakâŠ†â§ºâ‚‚ u)


-- Substitution.

[_â‰”_]_ : âˆ€ {A B Î“ Î”} â†’ (i : A âˆˆ Î“) â†’ Î“ âˆ– i â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ B â†’ Î“ âˆ– i â Î” âŠ¢ B
[ i â‰” s ] var j     with i â‰Ÿâˆˆ j
[ i â‰” s ] var .i    | same   = s
[ i â‰” s ] var ._    | diff j = var j
[ i â‰” s ] lam t     = lam ([ pop i â‰” monoâŠ¢ weakâŠ† s ] t)
[ i â‰” s ] app t u   = app ([ i â‰” s ] t) ([ i â‰” s ] u)
[ i â‰” s ] mvar j    = mvar j
[ i â‰” s ] box t     = box t
[ i â‰” s ] unbox t u = unbox ([ i â‰” s ] t) ([ i â‰” mmonoâŠ¢ weakâŠ† s ] u)
[ i â‰” s ] pair t u  = pair ([ i â‰” s ] t) ([ i â‰” s ] u)
[ i â‰” s ] fst t     = fst ([ i â‰” s ] t)
[ i â‰” s ] snd t     = snd ([ i â‰” s ] t)
[ i â‰” s ] tt        = tt

[_â‰”_]â‹†_ : âˆ€ {Î A Î“ Î”} â†’ (i : A âˆˆ Î“) â†’ Î“ âˆ– i â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î“ âˆ– i â Î” âŠ¢â‹† Î
[_â‰”_]â‹†_ {âˆ…}     i s âˆ™        = âˆ™
[_â‰”_]â‹†_ {Î , B} i s (ts , t) = [ i â‰” s ]â‹† ts , [ i â‰” s ] t


-- Modal substitution.

m[_â‰”_]_ : âˆ€ {A B Î“ Î”} â†’ (i : A âˆˆ Î”) â†’ âˆ… â Î” âˆ– i âŠ¢ A â†’ Î“ â Î” âŠ¢ B â†’ Î“ â Î” âˆ– i âŠ¢ B
m[ i â‰” s ] var j     = var j
m[ i â‰” s ] lam t     = lam (m[ i â‰” s ] t)
m[ i â‰” s ] app t u   = app (m[ i â‰” s ] t) (m[ i â‰” s ] u)
m[ i â‰” s ] mvar j    with i â‰Ÿâˆˆ j
m[ i â‰” s ] mvar .i   | same   = monoâŠ¢ botâŠ† s
m[ i â‰” s ] mvar ._   | diff j = mvar j
m[ i â‰” s ] box t     = box (m[ i â‰” s ] t)
m[ i â‰” s ] unbox t u = unbox (m[ i â‰” s ] t) (m[ pop i â‰” mmonoâŠ¢ weakâŠ† s ] u)
m[ i â‰” s ] pair t u  = pair (m[ i â‰” s ] t) (m[ i â‰” s ] u)
m[ i â‰” s ] fst t     = fst (m[ i â‰” s ] t)
m[ i â‰” s ] snd t     = snd (m[ i â‰” s ] t)
m[ i â‰” s ] tt        = tt

m[_â‰”_]â‹†_ : âˆ€ {Î A Î“ Î”} â†’ (i : A âˆˆ Î”) â†’ âˆ… â Î” âˆ– i âŠ¢ A â†’ Î“ â Î” âŠ¢â‹† Î â†’ Î“ â Î” âˆ– i âŠ¢â‹† Î
m[_â‰”_]â‹†_ {âˆ…}     i s âˆ™        = âˆ™
m[_â‰”_]â‹†_ {Î , B} i s (ts , t) = m[ i â‰” s ]â‹† ts , m[ i â‰” s ] t


-- Conversion.

data _â‹™_ {Î“ Î” : Cx Ty} : âˆ€ {A} â†’ Î“ â Î” âŠ¢ A â†’ Î“ â Î” âŠ¢ A â†’ Set where
  reflâ‹™      : âˆ€ {A} {t : Î“ â Î” âŠ¢ A}
                â†’ t â‹™ t
  transâ‹™     : âˆ€ {A} {t tâ€² tâ€³ : Î“ â Î” âŠ¢ A}
                â†’ t â‹™ tâ€² â†’ tâ€² â‹™ tâ€³ â†’ t â‹™ tâ€³
  symâ‹™       : âˆ€ {A} {t tâ€² : Î“ â Î” âŠ¢ A}
                â†’ t â‹™ tâ€² â†’ tâ€² â‹™ t
  conglamâ‹™   : âˆ€ {A B} {t tâ€² : Î“ , A â Î” âŠ¢ B}
                â†’ t â‹™ tâ€²
                â†’ lam t â‹™ lam tâ€²
  congappâ‹™   : âˆ€ {A B} {t tâ€² : Î“ â Î” âŠ¢ A â–» B} {u uâ€² : Î“ â Î” âŠ¢ A}
                â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
                â†’ app t u â‹™ app tâ€² uâ€²
  -- NOTE: Rejected by Pfenning and Davies.
  -- congboxâ‹™   : âˆ€ {A} {t tâ€² : âˆ… â Î” âŠ¢ A}
  --               â†’ t â‹™ tâ€²
  --               â†’ box {Î“} t â‹™ box {Î“} tâ€²
  congunboxâ‹™ : âˆ€ {A C} {t tâ€² : Î“ â Î” âŠ¢ â–¡ A} {u uâ€² : Î“ â Î” , A âŠ¢ C}
                â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
                â†’ unbox t u â‹™ unbox tâ€² uâ€²
  congpairâ‹™  : âˆ€ {A B} {t tâ€² : Î“ â Î” âŠ¢ A} {u uâ€² : Î“ â Î” âŠ¢ B}
                â†’ t â‹™ tâ€² â†’ u â‹™ uâ€²
                â†’ pair t u â‹™ pair tâ€² uâ€²
  congfstâ‹™   : âˆ€ {A B} {t tâ€² : Î“ â Î” âŠ¢ A âˆ§ B}
                â†’ t â‹™ tâ€²
                â†’ fst t â‹™ fst tâ€²
  congsndâ‹™   : âˆ€ {A B} {t tâ€² : Î“ â Î” âŠ¢ A âˆ§ B}
                â†’ t â‹™ tâ€²
                â†’ snd t â‹™ snd tâ€²
  betaâ–»â‹™     : âˆ€ {A B} {t : Î“ , A â Î” âŠ¢ B} {u : Î“ â Î” âŠ¢ A}
                â†’ app (lam t) u â‹™ ([ top â‰” u ] t)
  etaâ–»â‹™      : âˆ€ {A B} {t : Î“ â Î” âŠ¢ A â–» B}
                â†’ t â‹™ lam (app (monoâŠ¢ weakâŠ† t) vâ‚€)
  -- TODO: Verify this.
  betaâ–¡â‹™     : âˆ€ {A C} {t : âˆ… â Î” âŠ¢ A} {u : Î“ â Î” , A âŠ¢ C}
                â†’ unbox (box t) u â‹™ (m[ top â‰” t ] u)
  -- TODO: Verify this.
  etaâ–¡â‹™      : âˆ€ {A} {t : Î“ â Î” âŠ¢ â–¡ A}
                â†’ t â‹™ unbox t (box mvâ‚€)
  -- TODO: What about commuting conversions for â–¡?
  betaâˆ§â‚â‹™    : âˆ€ {A B} {t : Î“ â Î” âŠ¢ A} {u : Î“ â Î” âŠ¢ B}
                â†’ fst (pair t u) â‹™ t
  betaâˆ§â‚‚â‹™    : âˆ€ {A B} {t : Î“ â Î” âŠ¢ A} {u : Î“ â Î” âŠ¢ B}
                â†’ snd (pair t u) â‹™ u
  etaâˆ§â‹™      : âˆ€ {A B} {t : Î“ â Î” âŠ¢ A âˆ§ B}
                â†’ t â‹™ pair (fst t) (snd t)
  etaâŠ¤â‹™     : âˆ€ {t : Î“ â Î” âŠ¢ âŠ¤}
                â†’ t â‹™ tt
