----------------------------------------------------------------------------------------------------

-- normalization-by-evaluation to Î²-short semi-weak normal form, with explicit model

-- using an explicit recursion principle on types allows defining the model as a single record that
-- includes a `âŸ¦recâŸ§` field after the definition of `_âŠ©_`, but requires using copatterns and a
-- very specific order of definitions in the canonical model section

-- thanks to Ulf Norell

module A202401.STLC-Naturals-SWNF-NBE3 where

open import A202401.STLC-Naturals-SWNF public
open import A202401.Kit4 public


----------------------------------------------------------------------------------------------------

recTy : âˆ€ {ğ“} {X : Set ğ“} â†’ Ty â†’ (Ty â†’ X â†’ Ty â†’ X â†’ X) â†’ X â†’ X
recTy (A âŒœâŠƒâŒ B) fâŠƒ fâ„• = fâŠƒ A (recTy A fâŠƒ fâ„•) B (recTy B fâŠƒ fâ„•)
recTy âŒœâ„•âŒ       fâŠƒ fâ„• = fâ„•

record Model : Setâ‚ where
  infix 4 _â‰¤_
  field
    World  : Set
    _â‰¤_    : World â†’ World â†’ Set
    reflâ‰¤  : âˆ€ {W} â†’ W â‰¤ W
    transâ‰¤ : âˆ€ {W Wâ€² Wâ€³} â†’ W â‰¤ Wâ€² â†’ Wâ€² â‰¤ Wâ€³ â†’ W â‰¤ Wâ€³
    âŸ¦â„•âŸ§    : World â†’ Set
    renâŸ¦â„•âŸ§ : âˆ€ {W Wâ€²} â†’ W â‰¤ Wâ€² â†’ âŸ¦â„•âŸ§ W â†’ âŸ¦â„•âŸ§ Wâ€²
    âŸ¦zeroâŸ§ : âˆ€ {W} â†’ âŸ¦â„•âŸ§ W
    âŸ¦sucâŸ§  : âˆ€ {W} â†’ âŸ¦â„•âŸ§ W â†’ âŸ¦â„•âŸ§ W

  infix 3 _âŠ©_
  _âŠ©_ : World â†’ Ty â†’ Set
  W âŠ© A = recTy {X = World â†’ Set} A
             (Î» A recA B recB W â†’ âˆ€ {Wâ€²} â†’ W â‰¤ Wâ€² â†’ recA Wâ€² â†’ recB Wâ€²)
             (Î» W â†’ âŸ¦â„•âŸ§ W)
             W

  field
    âŸ¦recâŸ§ : âˆ€ {W A} â†’ W âŠ© âŒœâ„•âŒ â†’ W âŠ© A â†’ W âŠ© âŒœâ„•âŒ âŒœâŠƒâŒ A âŒœâŠƒâŒ A â†’ W âŠ© A

open Model public

module _ {â„³ : Model} where
  private
    module â„³ = Model â„³

  vren : âˆ€ {A W Wâ€²} â†’ W â„³.â‰¤ Wâ€² â†’ W â„³.âŠ© A â†’ Wâ€² â„³.âŠ© A
  vren {A âŒœâŠƒâŒ B} Ï± v = Î» Ï±â€² â†’ v (â„³.transâ‰¤ Ï± Ï±â€²)
  vren {âŒœâ„•âŒ}     Ï± v = â„³.renâŸ¦â„•âŸ§ Ï± v

open ModelKit (kit (Î» {â„³} â†’ _âŠ©_ â„³) (Î» {â„³} {A} â†’ vren {â„³} {A})) public

âŸ¦_âŸ§ : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Î“ âŠ¨ A
âŸ¦ var i                  âŸ§         Î³ = âŸ¦ i âŸ§âˆ‹ Î³
âŸ¦ âŒœÎ»âŒ t                  âŸ§         Î³ = Î» Ï± v â†’ âŸ¦ t âŸ§ (vrenÂ§ Ï± Î³ , v)
âŸ¦ tâ‚ âŒœ$âŒ tâ‚‚              âŸ§ {â„³ = â„³} Î³ = âŸ¦ tâ‚ âŸ§ Î³ (reflâ‰¤ â„³) $ âŸ¦ tâ‚‚ âŸ§ Î³
âŸ¦ âŒœzeroâŒ                 âŸ§ {â„³ = â„³} Î³ = âŸ¦zeroâŸ§ â„³
âŸ¦ âŒœsucâŒ t                âŸ§ {â„³ = â„³} Î³ = âŸ¦sucâŸ§ â„³ (âŸ¦ t âŸ§ Î³)
âŸ¦ âŒœrecâŒ {A = A} tâ‚™ tâ‚€ tâ‚› âŸ§ {â„³ = â„³} Î³ = âŸ¦recâŸ§ â„³ {A = A} (âŸ¦ tâ‚™ âŸ§ Î³) (âŸ¦ tâ‚€ âŸ§ Î³) Î» Ï± vâ‚™ Ï±â€² vâ‚ â†’
                                          âŸ¦ tâ‚› âŸ§ ((vrenÂ§ (transâ‰¤ â„³ Ï± Ï±â€²) Î³ , renâŸ¦â„•âŸ§ â„³ Ï±â€² vâ‚™) , vâ‚)


----------------------------------------------------------------------------------------------------

-- canonical model
ğ’ : Model

ğ’âŸ¦recâŸ§ : âˆ€ {W A} â†’ ğ’ / W âŠ© âŒœâ„•âŒ â†’ ğ’ / W âŠ© A â†’ ğ’ / W âŠ© âŒœâ„•âŒ âŒœâŠƒâŒ A âŒœâŠƒâŒ A â†’ ğ’ / W âŠ© A

ğ’ .World         = Ctx
ğ’ ._â‰¤_           = _âŠ‘_
ğ’ .reflâ‰¤         = reflâŠ‘
ğ’ .transâ‰¤        = transâŠ‘
ğ’ .âŸ¦â„•âŸ§           = Î» Î“ â†’ Î£ (Î“ âŠ¢ âŒœâ„•âŒ) NF
ğ’ .renâŸ¦â„•âŸ§        = Î» { e (_ , p) â†’ _ , renNF e p }
ğ’ .âŸ¦zeroâŸ§        = _ , âŒœzeroâŒ
ğ’ .âŸ¦sucâŸ§         = Î» { (_ , p) â†’ _ , âŒœsucâŒ p }
ğ’ .âŸ¦recâŸ§ {A = A} = ğ’âŸ¦recâŸ§ {A = A}

â†‘ : âˆ€ {A Î“} â†’ Î£ (Î“ âŠ¢ A) NNF â†’ ğ’ / Î“ âŠ© A

â†“ : âˆ€ {A Î“} â†’ ğ’ / Î“ âŠ© A â†’ Î£ (Î“ âŠ¢ A) NF

ğ’âŸ¦recâŸ§         (_ , âŒœzeroâŒ)   vâ‚€ vâ‚› = vâ‚€
ğ’âŸ¦recâŸ§         (_ , âŒœsucâŒ pâ‚™) vâ‚€ vâ‚› = vâ‚› idâŠ‘ (_ , pâ‚™) idâŠ‘ vâ‚€
ğ’âŸ¦recâŸ§ {A = A} (_ , nnf pâ‚™)   vâ‚€ vâ‚› = let _ , pâ‚€ = â†“ {A} vâ‚€
                                          _ , pâ‚› = â†“ (vâ‚› (wkâŠ‘ (wkâŠ‘ idâŠ‘))
                                                     (â†‘ {âŒœâ„•âŒ} (var (wkâˆ‹ zero) , var-))
                                                     idâŠ‘ (â†‘ {A} (var zero , var-)))
                                        in â†‘ (_ , âŒœrecâŒ pâ‚™ pâ‚€ pâ‚›)

â†‘ {A âŒœâŠƒâŒ B} (_ , pâ‚) = Î» e vâ‚‚ â†’ let _ , pâ‚‚ = â†“ vâ‚‚ in
                         â†‘ (_ , renNNF e pâ‚ âŒœ$âŒ pâ‚‚)
â†‘ {âŒœâ„•âŒ}     (_ , p)  = _ , nnf p

â†“ {A âŒœâŠƒâŒ B} v = let t , p = â†“ (v (wkâŠ‘ idâŠ‘) (â†‘ {A} (var zero , var-))) in
                  âŒœÎ»âŒ t , âŒœÎ»âŒ-
â†“ {âŒœâ„•âŒ}     v = v

vidÂ§ : âˆ€ {Î“} â†’ ğ’ / Î“ âŠ©Â§ Î“
vidÂ§ {âˆ™}     = âˆ™
vidÂ§ {Î“ , A} = vrenÂ§ (wkâŠ‘ idâŠ‘) vidÂ§ , â†‘ {A} (var zero , var-)

âŸ¦_âŸ§â»Â¹ : âˆ€ {Î“ A} â†’ Î“ âŠ¨ A â†’ Î£ (Î“ âŠ¢ A) NF
âŸ¦ v âŸ§â»Â¹ = â†“ (v vidÂ§)

nbe : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Î£ (Î“ âŠ¢ A) NF
nbe t = âŸ¦ âŸ¦ t âŸ§ âŸ§â»Â¹


----------------------------------------------------------------------------------------------------
